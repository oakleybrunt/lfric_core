!-----------------------------------------------------------------------------
! (C) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!>@brief Initialises IO_Dev fields to the default analytic value - the product
!>       of the x, y and z coordinate fields for any given function space
module io_dev_init_fields_alg_mod

  use field_mod,                    only : field_type
  use integer_field_mod,            only : integer_field_type
  use field_parent_mod,             only : field_parent_type
  use field_collection_mod,         only : field_collection_type, &
                                           field_collection_iterator_type
  use io_dev_init_kernel_mod,       only : io_dev_init_kernel_type
  use io_dev_init_int_kernel_mod,   only : io_dev_init_int_kernel_type
  use log_mod,                      only : LOG_LEVEL_INFO

  implicit none

  private
  public :: io_dev_init_fields_alg

contains
  !> @details An algorithm for initialising IO_Dev fields analytically
  !> @param[in,out] core_fields The core model field collection
  !> @param[in]     chi         A size 3 array of fields holding the mesh coordinates
  !> @param[in]     panel_id    Field holding the IDs of mesh panels
subroutine io_dev_init_fields_alg(core_fields, chi, panel_id)

    implicit none

    type(field_collection_type), intent(inout) :: core_fields
    type(field_type),            intent(in)    :: chi(3)
    type(field_type),            intent(in)    :: panel_id

    type(field_collection_iterator_type) :: iter

    class(field_parent_type), pointer :: fld => null()
    type(field_type), pointer :: fld_actual => null()
    type(integer_field_type), pointer :: integer_fld_actual => null()

    iter = core_fields%get_iterator()
    do
      if ( .not.iter%has_next() ) exit
      fld => iter%next()
      select type(fld)
        type is (field_type)
          ! Initialise fields to default analytic value (product of x, y and z
          ! coordinates)

          ! PSyclone needs to know the actual rather than abstract type
          fld_actual => fld
          call invoke ( setval_c(fld_actual, 0.0_r_def),                   &
                        io_dev_init_kernel_type(fld_actual, chi, panel_id) )

        type is (integer_field_type)
          ! Initialise fields to default analytic value (dof ID)

          ! PSyclone needs to know the actual rather than abstract type
          integer_fld_actual => fld
          call invoke( io_dev_init_int_kernel_type(integer_fld_actual) )

      end select
    end do

  end subroutine io_dev_init_fields_alg

end module io_dev_init_fields_alg_mod
