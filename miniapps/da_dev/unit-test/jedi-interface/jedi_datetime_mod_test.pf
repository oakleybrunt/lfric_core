!-----------------------------------------------------------------------------
! (c) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> LFRic unit tests jedi_datetime_mod.
!> LFRic unit tests use pfUnit, a Fortran unit test framework written by NASA.
!> This particular example tests the result of a matrix vector calculation
!> using the code from the matrix_vector_kernel_mod in the LFRic science
!> component.
!>
!-------------------------------------------------------------------------------
module jedi_datetime_mod_test

  ! Global use statements go here

  use constants_mod, only : i_def, str_def, l_def

  use pFUnit_Mod

  implicit none

  private
  public :: jedi_datetime_test

  ! The test case type, containing procedures to setup, run
  ! and clean up after a test.
  ! It can also contain data.

  @TestCase
  type, extends(TestCase), public :: jedi_datetime_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure jedi_datetime_test
  end type jedi_datetime_test_type


contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  !> The setUp subroutine is optional and is called prior to running the test.
  !> It is used to create and initialise types / data that are used in
  !> the tests
  subroutine setUp( this )

    implicit none

    class(jedi_datetime_test_type), intent(inout) :: this

    ! This section should create and initialise anything needed for the test

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  !> The tearDown subroutine is optional and called after the test.
  !> It is used to destroy any types / data previously created by setUp()
  subroutine tearDown( this )


    implicit none

    class(jedi_datetime_test_type), intent(inout) :: this

    ! This section should destroy any previously created types / data

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  ! pfUnit will run each subroutine preceded by the @test line
  @test
  !> Test the emulated jedi_datetime type
  subroutine jedi_datetime_test( this )

    use jedi_datetime_mod, only : jedi_datetime_type

    implicit none

    class(jedi_datetime_test_type), intent(inout) :: this

    type(jedi_datetime_type) :: jedi_datetime
    type(jedi_datetime_type) :: jedi_datetime_2
    type(jedi_datetime_type) :: jedi_datetime_3

    integer(i_def)           :: YYYYMMDD, hhmmss
    integer(i_def)           :: year, month, day
    integer(i_def)           :: hour, minute, second
    integer(i_def)           :: difference_seconds
    character(str_def)       :: iso_datetime, returned_iso_datetime

    integer(i_def) :: date, returned_date
    integer(i_def) :: time, returned_time
    logical(l_def) :: ahead

    date = 2460040_i_def
    time = 58770_i_def

    ! Test init with YYYYMMDD and hhmmss
    YYYYMMDD = 20230405_i_def
    hhmmss   = 161930_i_def

    call jedi_datetime%init( YYYYMMDD, hhmmss )
    call jedi_datetime%get_date( returned_date )
    @assertEqual( returned_date, date )
    call jedi_datetime%get_time( returned_time )
    @assertEqual( returned_time, time )

    ! Test init with year, month, day, hour, minute, second
    year   = 2023_i_def
    month  = 04_i_def
    day    = 05_i_def
    hour   = 16_i_def
    minute = 19_i_def
    second = 30_i_def

    call jedi_datetime%init( year, month, day, hour, minute, second )
    call jedi_datetime%get_date( returned_date )
    @assertEqual( returned_date, date )
    call jedi_datetime%get_time( returned_time )
    @assertEqual( returned_time, time )

    ! Test init with iso datetime string
    iso_datetime = '2023-04-05T16:19:30Z'
    call jedi_datetime_2%init( iso_datetime )
    call jedi_datetime_2%get_date( returned_date )
    @assertEqual( returned_date, date )
    call jedi_datetime_2%get_time( returned_time )
    @assertEqual( returned_time, time )

    ! Test init from another jedi_datetime instance
    call jedi_datetime_3%init( jedi_datetime_2 )
    call jedi_datetime_3%get_date( returned_date )
    @assertEqual( returned_date, date )
    call jedi_datetime_3%get_time( returned_time )
    @assertEqual( returned_time, time )

    ! Test adding negative amount of seconds
    call jedi_datetime_2%add_seconds( -1_i_def )
    call jedi_datetime_2%get_time( returned_time )
    @assertEqual( returned_time, time - 1_i_def )

    ! Test adding seconds
    call jedi_datetime_2%add_seconds( 2_i_def )
    call jedi_datetime_2%get_time( returned_time )
    @assertEqual( returned_time, time + 1_i_def )

    ! Test getting difference between 2 datetimes
    call jedi_datetime%seconds_between( jedi_datetime_2, difference_seconds )
    @assertEqual( difference_seconds, 1_i_def )

    ! Test getting difference between 2 datetimes
    call jedi_datetime_2%seconds_between( jedi_datetime, difference_seconds )
    @assertEqual( difference_seconds, -1_i_def )

    ! Test case if over a days difference
    iso_datetime = '2023-04-06T16:19:32Z'
    call jedi_datetime_2%init( iso_datetime )
    call jedi_datetime%seconds_between( jedi_datetime_2, difference_seconds )
    @assertEqual( difference_seconds, 86402_i_def )

    ! Test is ahead (datetime_2 is ahead of datetime)
    ahead = jedi_datetime_2%is_ahead( jedi_datetime )
    @assertEqual( ahead, .true. )
    ahead = jedi_datetime%is_ahead( jedi_datetime_2 )
    @assertEqual( ahead, .false. )
    ! update datetime_3 so both have the same datetime
    call jedi_datetime_3%init( jedi_datetime_2 )
    ahead = jedi_datetime_3%is_ahead( jedi_datetime_2 )
    @assertEqual( ahead, .false. )

    ! Test getting iso string back
    call jedi_datetime_2%to_string( returned_iso_datetime )
    @assertEqual( returned_iso_datetime, '2023-04-06 16:19:32' )

  end subroutine jedi_datetime_test

end module jedi_datetime_mod_test
