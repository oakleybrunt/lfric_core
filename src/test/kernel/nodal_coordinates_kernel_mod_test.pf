!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

module nodal_coordinates_kernel_mod_test

  implicit none 


contains

  @test
  subroutine nodal_coordinates_kernel_test()

    use pFUnit_Mod
    use field_mod,                  only : field_type,      &
                                           field_proxy_type
    use function_space_mod,         only : function_space_type &
                                         , purge_function_spaces  
    use fs_continuity_mod,          only : W0, W2
    use configuration_mod,          only : QUAD
    use constants_mod,              only : r_def
    use quadrature_mod,             only : quadrature_type, GAUSSIAN
    use mesh_mod,                   only : mesh_type, PLANE_BI_PERIODIC
    use reference_element_mod,      only : reference_cube, deallocate_reference &
                                         , reference_element
    

    use nodal_coordinates_kernel_mod, only: nodal_coordinates_code
    implicit none

    type(function_space_type) :: fs
    type( quadrature_type)    :: qr
    type(mesh_type)  :: mesh

    real(kind=r_def) :: tol, answer

    integer :: i, j, k, cell

    ! Variables for creating the chi fields
    real(kind=r_def), parameter :: dx = 6000.0_r_def, &
                                   dy = 1000.0_r_def, &
                                   dz = 2000.0_r_def
    ! Variables for testing kernel
    type(field_type) :: x(3)
    type(field_type) :: chi(3)
 
    type(field_proxy_type) :: x_p(3), chi_p(3)
   
    integer                 :: nlayers
    integer                 :: ndf_chi, ndf_x
    integer                 :: undf_chi, undf_x
    integer                 :: dim_chi
    integer                 :: element_order
    integer, pointer        :: map_chi(:), map_x(:) => null()

    real(kind=r_def), allocatable  :: basis_chi(:,:,:)
    real(kind=r_def), pointer :: nodes(:,:) => null()
 
    ! Setup
    tol = 1.0e-6_r_def  
    qr = quadrature_type(3, GAUSSIAN)

    reference_element = QUAD
    call reference_cube()

    mesh = mesh_type(PLANE_BI_PERIODIC)

    ! Set element order
    element_order = 0

    ! Insert Psykal_lite unpacking code here
    do i = 1,3
      x(i)     = field_type( vector_space = fs%get_instance( mesh, element_order, W2 ) )
      chi(i)   = field_type( vector_space = fs%get_instance( mesh, element_order, W0 ) )
      x_p(i)   = x(i)%get_proxy()
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3          
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    nlayers = x_p(1)%vspace%get_nlayers()

    ndf_x  = x_p(1)%vspace%get_ndf( )
    undf_x = x_p(1)%vspace%get_undf()

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf()

    dim_chi = chi_p(1)%vspace%get_dim_space( )
    allocate(basis_chi(dim_chi, ndf_chi, ndf_x))

    nodes => x_p(1)%vspace%get_nodes( )
    call chi_p(1)%vspace%compute_nodal_basis_function(basis_chi, ndf_chi, ndf_x, nodes)    

    ! Create the data   
    x_p(1)%data(:) = 0.0_r_def
    x_p(2)%data(:) = 0.0_r_def
    x_p(3)%data(:) = 0.0_r_def

    ! Call the kernel    
    cell = 3
    map_x   => x_p(1)%vspace%get_cell_dofmap( cell )
    map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
    call nodal_coordinates_code(nlayers, &
                                x_p(1)%data, &
                                x_p(2)%data, &
                                x_p(3)%data, &
                                chi_p(1)%data, &
                                chi_p(2)%data, &
                                chi_p(3)%data, &
                                ndf_x, undf_x, map_x, &
                                ndf_chi, undf_chi, map_chi, &
                                basis_chi &
                               )
     
    !Test the answer
    answer = chi_p(1)%data(map_chi(1))
    @assertEqual(answer, x_p(1)%data(map_x(1)), tol)
    answer = 0.5_r_def*(chi_p(2)%data(map_chi(1))+chi_p(2)%data(map_chi(4)))
    @assertEqual(answer, x_p(2)%data(map_x(1)), tol)
    answer = chi_p(3)%data(map_chi(5))
    @assertEqual(answer, x_p(3)%data(map_x(6)), tol)

    call deallocate_reference()
    call purge_function_spaces()

  
  end subroutine nodal_coordinates_kernel_test

end module nodal_coordinates_kernel_mod_test
