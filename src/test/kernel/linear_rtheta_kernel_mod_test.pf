!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the linear_rtheta computation
!>
module linear_rtheta_kernel_mod_test

  implicit none 


contains

  @test
  subroutine linear_rtheta_kernel_test()

    use pFUnit_Mod
    use field_mod,                  only : field_type,      &
                                           field_proxy_type
    use linear_rtheta_kernel_mod, &
                                    only : linear_rtheta_code
    use constants_mod,              only : i_def, r_def, GRAVITY
    use initialisation_mod,         only : n_sq
    use quadrature_mod,             only : quadrature_type, GAUSSIAN

    use configuration_mod,     only: QUAD, element_order
    use mesh_mod,              only: mesh_type, PLANE_BI_PERIODIC
    use function_space_mod,    only: function_space_type                       &
                                   , purge_function_spaces
    use fs_continuity_mod,     only: W0, W2
    use reference_element_mod, only: reference_cube, deallocate_reference      &
                                   , reference_element

    implicit none

    type(field_type) :: chi(3)
    type(field_proxy_type )   :: chi_p(3) 

    type(mesh_type)           :: mesh
    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()



    real(kind=r_def) :: tol
    real(kind=r_def), dimension(:), allocatable :: answer

    integer :: i, j, k, cell
    integer(i_def) :: err

    real(kind=r_def), parameter :: dx = 6000.0_r_def, &
                                   dy = 1000.0_r_def
    real(r_def) :: dz, dz_test(3)

    ! Required for calling kernel 
    type( field_type ) :: r_theta, u, phi

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, undf_w0, dim_w0, dim_diff_w0
    integer                 :: ndf_w2, undf_w2, dim_w2
    integer, pointer        :: map_w2(:), map_w0(:) => null()

    type( field_proxy_type )        :: r_theta_proxy, u_proxy, phi_proxy 
    
    real(kind=r_def), allocatable  :: basis_w2(:,:,:,:), &
                                      basis_w0(:,:,:,:), &
                                      diff_basis_w0(:,:,:,:) 

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
   
    tol = 1.0e-6_r_def 
    n_sq = 0.0001_r_def
    qr  = quadrature_type(3, GAUSSIAN)

    reference_element = QUAD
    call reference_cube()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh = mesh_type(PLANE_BI_PERIODIC)

    call mesh%get_dz(dz_test)
    dz = dz_test(1)

    w0_fs => fs%get_instance( mesh, element_order, W0 )
    w2_fs => fs%get_instance( mesh, element_order, W2 )

    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i) = field_type( vector_space = w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3          
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    r_theta = field_type( vector_space = w0_fs )
    phi     = field_type( vector_space = w0_fs )
    u       = field_type( vector_space = w2_fs )

    r_theta_proxy   = r_theta%get_proxy()
    u_proxy         = u%get_proxy()
    phi_proxy    = phi%get_proxy()

    nlayers = r_theta_proxy%vspace%get_nlayers()

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    ndf_w2      = u_proxy%vspace%get_ndf( )
    dim_w2      = u_proxy%vspace%get_dim_space( )
    undf_w2     = u_proxy%vspace%get_undf()
    allocate(basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v))

    ndf_w0      = r_theta_proxy%vspace%get_ndf( )
    dim_w0      = r_theta_proxy%vspace%get_dim_space( )
    dim_diff_w0 = r_theta_proxy%vspace%get_dim_space_diff( )
    undf_w0     = r_theta_proxy%vspace%get_undf()
    allocate(basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v))
    allocate(diff_basis_w0(dim_diff_w0,ndf_w0,nqp_h,nqp_v))

    call u_proxy%vspace%compute_basis_function(                     &
        basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)    

    call r_theta_proxy%vspace%compute_basis_function(basis_w0, ndf_w0,      & 
         nqp_h, nqp_v, xp, zp)    

    call r_theta_proxy%vspace%compute_diff_basis_function(                  &
         diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    
    cell = 1
    map_w0 => r_theta_proxy%vspace%get_cell_dofmap( cell )
    map_w2 => u_proxy%vspace%get_cell_dofmap( cell )

    ! Create the data
    r_theta_proxy%data(:) = 0.0_r_def
    u_proxy%data(:) = 0.0_r_def
    phi_proxy%data(:) = GRAVITY*chi_p(3)%data(:)

    call linear_rtheta_code( nlayers, &
                             r_theta_proxy%data, &
                             u_proxy%data, &                        
                             phi_proxy%data, &
                             chi_p(1)%data, &
                             chi_p(2)%data, &
                             chi_p(3)%data,  &
                             ndf_w0, undf_w0, &
                             map_w0, &
                             basis_w0, &
                             diff_basis_w0, &   
                             ndf_w2, undf_w2, &
                             map_w2, &
                             basis_w2, &
                             nqp_h, nqp_v, wh, wv &
                             )

    deallocate(basis_w2, basis_w0, diff_basis_w0)    
    allocate( answer(undf_w0) )

    ! Simple test, check that when u=0 the rhs=0    
    answer(:) = 0.0_r_def

    @assertEqual(answer, r_theta_proxy%data(:), tol)

    deallocate( answer )
    
    ! Tear down ...
    call deallocate_reference()
    call purge_function_spaces()

  end subroutine linear_rtheta_kernel_test

end module linear_rtheta_kernel_mod_test
