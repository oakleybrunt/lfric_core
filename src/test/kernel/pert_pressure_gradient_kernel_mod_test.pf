!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

module pert_pressure_gradient_kernel_mod_test

  implicit none 


contains

  @test
  subroutine pert_pressure_gradient_kernel_test()

    use pFUnit_Mod
    use field_mod,            only: field_type, field_proxy_type
    use pert_pressure_gradient_kernel_mod, &
                                   only : pert_pressure_gradient_code
    use constants_mod,             only : i_def, r_def, GRAVITY
    use quadrature_mod,            only : quadrature_type, GAUSSIAN
    use initialisation_mod,        only : n_sq
    use reference_profile_mod,     only : reference_profile
 
    use configuration_mod,     only: l_spherical, QUAD, element_order
    use mesh_mod,              only: mesh_type, PLANE_BI_PERIODIC
    use function_space_mod,    only: function_space_type                       &
                                   , purge_function_spaces
    use fs_continuity_mod,     only: W0, W2, W3
    use reference_element_mod, only: reference_cube, deallocate_reference      &
                                   , reference_element

    implicit none

    type(field_type) :: chi(3)
    type(field_proxy_type )   :: chi_p(3) 

    type(mesh_type)           :: mesh
    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()




    real(kind=r_def) :: tol
    real(kind=r_def) :: answer

    integer :: i, j, k, cell
    integer(i_def) :: err

    real(kind=r_def), parameter :: dx = 6000.0_r_def, &
                                   dy = 1000.0_r_def, &
                                   dz = 2000.0_r_def
    ! Required for calling kernel 
    type( field_type ) :: r_u, rho, theta, rho_ref, theta_ref

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, ndf_w2, ndf_w3
    integer                 :: undf_w0, undf_w2, undf_w3
    integer                 :: dim_w0, diff_dim_w0, dim_w2, diff_dim_w2, dim_w3
    integer, pointer        :: map_w3(:), map_w2(:), map_w0(:) => null()

    type( field_proxy_type )        :: r_u_proxy, rho_proxy, theta_proxy, &
                                       rho_ref_proxy, theta_ref_proxy
                                       
    
    real(kind=r_def), allocatable  :: basis_w3(:,:,:,:), &
                                      basis_w2(:,:,:,:), &
                                      basis_w0(:,:,:,:), &
                                      diff_basis_w0(:,:,:,:), &
                                      diff_basis_w2(:,:,:,:) 

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
   
    tol = 1.0e-3_r_def  
    l_spherical = .false. 
    n_sq        = 0.0001_r_def 
    qr = quadrature_type(3, GAUSSIAN)

    reference_element = QUAD
    call reference_cube()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh = mesh_type(PLANE_BI_PERIODIC)

    w0_fs => fs%get_instance( mesh, element_order, W0 )
    w2_fs => fs%get_instance( mesh, element_order, W2 )
    w3_fs => fs%get_instance( mesh, element_order, W3 )



    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i) = field_type( vector_space = w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3          
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    theta     = field_type( vector_space = w0_fs )
    theta_ref = field_type( vector_space = w0_fs )
    r_u       = field_type( vector_space = w2_fs )
    rho       = field_type( vector_space = w3_fs )
    rho_ref   = field_type( vector_space = w3_fs )


    r_u_proxy  = r_u%get_proxy()
    rho_proxy  = rho%get_proxy()
    theta_proxy = theta%get_proxy()
    rho_ref_proxy  = rho_ref%get_proxy()
    theta_ref_proxy = theta_ref%get_proxy()
    
    nlayers = rho_proxy%vspace%get_nlayers()
    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    ndf_w3  = rho_proxy%vspace%get_ndf( )
    dim_w3  = rho_proxy%vspace%get_dim_space( )
    undf_w3 = rho_proxy%vspace%get_undf()
    allocate(basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v))

    ndf_w2      = r_u_proxy%vspace%get_ndf( )
    dim_w2      = r_u_proxy%vspace%get_dim_space( )
    diff_dim_w2 = r_u_proxy%vspace%get_dim_space_diff( )
    undf_w2     = r_u_proxy%vspace%get_undf()
    allocate(basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v))
    allocate(diff_basis_w2(diff_dim_w2,ndf_w2,nqp_h,nqp_v))

    ndf_w0      = theta_proxy%vspace%get_ndf( )
    dim_w0      = theta_proxy%vspace%get_dim_space( )
    diff_dim_w0 = theta_proxy%vspace%get_dim_space_diff( )
    undf_w0     = theta_proxy%vspace%get_undf()
    allocate(basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v))
    allocate(diff_basis_w0(diff_dim_w0,ndf_w0,nqp_h,nqp_v))

    call rho_proxy%vspace%compute_basis_function( &
         basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp)    

    call r_u_proxy%vspace%compute_basis_function( &
         basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)    

    call r_u_proxy%vspace%compute_diff_basis_function( &
         diff_basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)

    call theta_proxy%vspace%compute_basis_function( &
         basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)    

    call theta_proxy%vspace%compute_diff_basis_function( &
         diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    cell = 1

    map_w3 => rho_proxy%vspace%get_cell_dofmap( cell )
    map_w2 => r_u_proxy%vspace%get_cell_dofmap( cell )
    map_w0 => theta_proxy%vspace%get_cell_dofmap( cell )

    ! Create the data
    r_u_proxy%data(:) = 0.0_r_def
    rho_proxy%data(:) = 0.0_r_def
    theta_proxy%data(:) = 0.0_r_def
    rho_ref_proxy%data(:) = 1.5_r_def
    theta_ref_proxy%data(:) = 300.0_r_def


    call pert_pressure_gradient_code( nlayers,                                 &
                                      r_u_proxy%data,                          &
                                      rho_proxy%data,                          &
                                      rho_ref_proxy%data,                      &
                                      theta_proxy%data,                        &
                                      theta_ref_proxy%data,                    &
                                      ndf_w2, undf_w2,                         &
                                      map_w2, basis_w2, diff_basis_w2,         &
                                      ndf_w3, undf_w3,                         &
                                      map_w3, basis_w3,                        &
                                      ndf_w0, undf_w0,                         &
                                      map_w0, basis_w0, diff_basis_w0,         &
                                      nqp_h, nqp_v, wh, wv                     &
                                      )           
    deallocate(basis_w3, basis_w2, diff_basis_w2, &
               basis_w0, diff_basis_w0)
    answer = 0.0_r_def
    k = 1
    @assertEqual(answer, r_u_proxy%data(map_w2(5)+k), tol)
    @assertEqual(answer, r_u_proxy%data(map_w2(6)+k), tol)

    ! Tear down ...
    call deallocate_reference()
    call purge_function_spaces()

  end subroutine pert_pressure_gradient_kernel_test

end module pert_pressure_gradient_kernel_mod_test
