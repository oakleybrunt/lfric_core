!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the rtheta computation
!>
module rtheta_kernel_mod_test

  implicit none 


contains

  @test
  subroutine rtheta_kernel_test()

    use pFUnit_Mod
    use field_mod,                  only : field_type,      &
                                           field_proxy_type
    use rtheta_kernel_mod,          only : rtheta_code
    use constants_mod,              only : i_def, r_def
    use quadrature_mod,             only : quadrature_type, GAUSSIAN

    use configuration_mod,     only: QUAD, element_order
    use mesh_mod,              only: mesh_type, PLANE_BI_PERIODIC
    use function_space_mod,    only: function_space_type                       &
                                   , purge_function_spaces
    use fs_continuity_mod,     only: W0, W2, W3
    use reference_element_mod, only: reference_cube, deallocate_reference      &
                                   , reference_element

    implicit none

    type(mesh_type)           :: mesh
    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()


    real(kind=r_def) :: tol
    real(kind=r_def), allocatable, dimension(:) :: answer

    integer :: cell
    integer(i_def) :: err

    ! Required for calling kernel 
    type( field_type )  :: r_theta, theta, f, rho

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, undf_w0, dim_w0, dim_diff_w0
    integer                 :: ndf_w2, undf_w2, dim_w2
    integer                 :: ndf_w3, undf_w3, dim_w3

    integer, pointer        :: map_w0(:) => null()
    integer, pointer        :: map_w2(:) => null()
    integer, pointer        :: map_w3(:) => null()

    type( field_proxy_type )        :: r_theta_proxy, theta_proxy, &
                                       f_proxy, rho_proxy
    
    real(kind=r_def), allocatable  :: basis_w2(:,:,:,:), &
                                      basis_w0(:,:,:,:), &
                                      basis_w3(:,:,:,:), &
                                      diff_basis_w0(:,:,:,:)

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:)   => null(), &
                                 wv(:)   => null()

   
    tol = 1.0e-6_r_def  
    qr  = quadrature_type(3, GAUSSIAN)

    reference_element = QUAD
    call reference_cube()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh = mesh_type(PLANE_BI_PERIODIC)

    w0_fs => fs%get_instance( mesh, element_order, W0 )
    w2_fs => fs%get_instance( mesh, element_order, W2 )
    w3_fs => fs%get_instance( mesh, element_order, W3 )

    ! Test kernel
    r_theta = field_type( vector_space = w0_fs )
    theta   = field_type( vector_space = w0_fs )
    f       = field_type( vector_space = w2_fs )
    rho     = field_type( vector_space = w3_fs )

    r_theta_proxy   = r_theta%get_proxy()
    theta_proxy     = theta%get_proxy()
    f_proxy         = f%get_proxy()
    rho_proxy       = rho%get_proxy()

    nlayers = r_theta_proxy%vspace%get_nlayers()

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    ndf_w2      = f_proxy%vspace%get_ndf( )
    dim_w2      = f_proxy%vspace%get_dim_space( )
    undf_w2     = f_proxy%vspace%get_undf()
    allocate(basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v))

    ndf_w0      = r_theta_proxy%vspace%get_ndf( )
    dim_w0      = r_theta_proxy%vspace%get_dim_space( )
    dim_diff_w0 = r_theta_proxy%vspace%get_dim_space_diff( )
    undf_w0     = r_theta_proxy%vspace%get_undf()
    allocate(basis_w0(dim_w0,ndf_w0,nqp_h,nqp_v))
    allocate(diff_basis_w0(dim_diff_w0,ndf_w0,nqp_h,nqp_v))

    ndf_w3      = rho_proxy%vspace%get_ndf( )
    dim_w3      = rho_proxy%vspace%get_dim_space( )
    undf_w3     = rho_proxy%vspace%get_undf()
    allocate(basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v))


    call f_proxy%vspace%compute_basis_function(                     &
        basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)    

    call r_theta_proxy%vspace%compute_basis_function(basis_w0, ndf_w0,      & 
         nqp_h, nqp_v, xp, zp)

    call r_theta_proxy%vspace%compute_diff_basis_function(                  &
         diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp)

    call rho_proxy%vspace%compute_basis_function(                     &
        basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp)    


    ! Create the data
    r_theta_proxy%data(:) = 0.0_r_def
    theta_proxy%data(:) = 300.0_r_def
    f_proxy%data(:) = 2.0_r_def
    rho_proxy%data(:) = 0.5_r_def


    do cell = 1, r_theta_proxy%vspace%get_ncell()
       map_w0 => r_theta_proxy%vspace%get_cell_dofmap( cell )
       map_w2 => f_proxy%vspace%get_cell_dofmap( cell )
       map_w3 => rho_proxy%vspace%get_cell_dofmap( cell )


       call rtheta_code( nlayers, &
                         r_theta_proxy%data, &
                         theta_proxy%data, &
                         f_proxy%data, &
                         rho_proxy%data, &
                         ndf_w0, undf_w0, &
                         map_w0, &
                         basis_w0, &
                         diff_basis_w0, &
                         ndf_w2, undf_w2, &
                         map_w2, &
                         basis_w2, &
                         ndf_w3, undf_w3, &
                         map_w3, &
                         basis_w3, &
                         nqp_h, nqp_v, wh, wv &
                         )
    end do 

    deallocate(basis_w2, basis_w0, diff_basis_w0, basis_w3)
    allocate( answer(undf_w0) )

    ! Simple test, check that when theta=const, f/rho.grad(theta) = 0    
    answer(:) = 0.0_r_def
    @assertEqual(answer, r_theta_proxy%data(:), tol)

    deallocate( answer )

    ! Tear down ...
    call deallocate_reference()
    call purge_function_spaces()

  end subroutine rtheta_kernel_test

end module rtheta_kernel_mod_test
