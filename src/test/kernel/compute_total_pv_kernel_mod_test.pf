!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the vorticity rhs computation
!>
module compute_total_pv_kernel_mod_test

  implicit none 


contains

  @test
  subroutine compute_total_pv_kernel_test()

    use pFUnit_Mod
    use field_mod,                  only : field_type,      &
                                           field_proxy_type
    use compute_total_pv_kernel_mod,only : compute_total_pv_code
    use constants_mod,              only : i_def, r_def
    use quadrature_mod,             only : quadrature_type, GAUSSIAN

    use configuration_mod,     only: QUAD, element_order
    use mesh_mod,              only: mesh_type, PLANE_BI_PERIODIC
    use function_space_mod,    only: function_space_type                       &
                                   , purge_function_spaces
    use fs_continuity_mod,     only: W0, W1, W3
    use reference_element_mod, only: reference_cube, deallocate_reference      &
                                   , reference_element

    implicit none

    type(field_type) :: chi(3)
    type(field_proxy_type )   :: chi_p(3) 

    type(mesh_type)           :: mesh
    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w1_fs => null()
    type(function_space_type), pointer :: w3_fs => null()



    real(kind=r_def) :: tol, answer

    integer :: i, j, k, cell
    integer(i_def) :: err

    ! Required for calling vorticity kernel    
    type( field_type )     :: xi, theta, pv

    integer          :: ndf_xi, undf_xi, dim_xi, &
                        ndf_theta, undf_theta, dim_diff_theta, &
                        ndf_pv, undf_pv, &
                        nqp_h, nqp_v


    integer, pointer :: map_xi(:) => null(), &
                        map_theta(:) => null(), & 
                        map_chi(:) => null(), & 
                        map_pv(:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()  

    type( field_proxy_type )        :: xi_p, theta_p, pv_p
    
    real(kind=r_def), dimension(:,:,:,:), allocatable :: &
                                  basis_xi, &
                                  diff_basis_theta
 
    tol = 1.0e-6_r_def 
    qr  = quadrature_type(3, GAUSSIAN)

    reference_element = QUAD
    call reference_cube()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh = mesh_type(PLANE_BI_PERIODIC)

    w0_fs => fs%get_instance( mesh, element_order, W0 )
    w1_fs => fs%get_instance( mesh, element_order, W1 )
    w3_fs => fs%get_instance( mesh, element_order, W3 )    

    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i) = field_type( vector_space = w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3          
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*4.0_r_def
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*3.0_r_def
          chi_p(3)%data(map_chi(1) + k) = real(k)  *2.0_r_def
        end do
        cell = cell + 1
      end do
    end do

    ! Test pv computation
    theta = field_type( vector_space = w0_fs )
    xi    = field_type( vector_space = w1_fs )
    pv    = field_type( vector_space = w3_fs )

    xi_p    = xi%get_proxy()
    theta_p = theta%get_proxy()
    pv_p    = pv%get_proxy()

    ndf_pv = pv_p%vspace%get_ndf( )
    undf_pv = pv_p%vspace%get_undf( )

    ndf_xi  = xi_p%vspace%get_ndf( )
    undf_xi = xi_p%vspace%get_undf( )
    dim_xi  = xi_p%vspace%get_dim_space( ) 

    ndf_theta      = theta_p%vspace%get_ndf( )
    undf_theta     = theta_p%vspace%get_undf( )
    dim_diff_theta = theta_p%vspace%get_dim_space_diff( ) 

    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    allocate( basis_xi(dim_xi, ndf_xi, nqp_h, nqp_v), &
              diff_basis_theta(dim_diff_theta, ndf_theta, nqp_h, nqp_v) )         
    
    call xi_p%vspace%compute_basis_function( &
         basis_xi, ndf_xi, nqp_h, nqp_v, xp, zp)           
    call theta_p%vspace%compute_diff_basis_function( &
         diff_basis_theta, ndf_theta, nqp_h, nqp_v, xp, zp)
           
    cell = 5
    map_xi    => xi_p%vspace%get_cell_dofmap( cell )
    map_theta => theta_p%vspace%get_cell_dofmap( cell )
    map_pv    => pv_p%vspace%get_cell_dofmap( cell )

    ! Create the data
    xi_p%data(:) = 6.0_r_def
    theta_p%data(:) = 300.0_r_def
    pv_p%data(:) = 0.0_r_def

    call compute_total_pv_code(                                   &
                               xi_p%vspace%get_nlayers(),         &
                               pv_p%data,                         &
                               xi_p%data,                         &
                               theta_p%data,                      &
                               chi_p(1)%data,                     &
                               chi_p(2)%data,                     &
                               chi_p(3)%data,                     &
                               ndf_pv, undf_pv,                   &
                               map_pv,                            &
                               ndf_xi, undf_xi,                   &
                               map_xi,                            &
                               basis_xi,                          &
                               ndf_theta, undf_theta,             &
                               map_theta,                         &
                               diff_basis_theta,                  &
                               nqp_h, nqp_v,                      &
                               wh, wv)

    deallocate( basis_xi, diff_basis_theta )

    ! This is only testing the vertical component of vorticity
    answer = 0.0_r_def
    @assertEqual(answer, pv_p%data(map_pv(1)), tol)

    ! Tear down ...
    call deallocate_reference()
    call purge_function_spaces()

  end subroutine compute_total_pv_kernel_test

end module compute_total_pv_kernel_mod_test
