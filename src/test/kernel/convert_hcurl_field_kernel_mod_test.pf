!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

module convert_hcurl_field_kernel_mod_test

  implicit none 


contains

  @test
  subroutine convert_hcurl_field_kernel_test()

    use pFUnit_Mod
    use field_mod,                  only : field_type,      &
                                           field_proxy_type
    use function_space_mod,         only : function_space_type &
                                         , purge_function_spaces
    use fs_continuity_mod,          only : W0, W1
    use configuration_mod,          only : QUAD
    use constants_mod,              only : r_def
    use quadrature_mod,             only : quadrature_type, GAUSSIAN
    use mesh_mod,                   only : mesh_type, PLANE_BI_PERIODIC
    use reference_element_mod,      only : reference_cube, deallocate_reference &
                                         , reference_element

    use convert_hcurl_field_kernel_mod, only: convert_hcurl_field_code
    implicit none

    type(function_space_type) :: fs
    type( quadrature_type)    :: qr
    type(mesh_type)  :: mesh

    real(kind=r_def) :: tol, answer

    integer :: i, j, k, cell

    integer :: element_order

    ! Variables for creating the chi fields
    real(kind=r_def), parameter :: dx = 6000.0_r_def, &
                                   dy = 1000.0_r_def, &
                                   dz = 2000.0_r_def
    ! Variables for testing kernel
    type(field_type) :: chi(3)
 
    type(field_proxy_type) :: chi_p(3)
   
    type(field_type) :: phys_field(3)
    type(field_type) :: comp_field
 
    type(field_proxy_type) :: phys_p(3), comp_p
   
    integer                 :: nlayers
    integer                 :: ndf_chi, ndf
    integer                 :: undf_chi, undf
    integer                 :: diff_dim_chi, dim
    integer, pointer        :: map_chi(:), map(:) => null()

    real(kind=r_def), allocatable  :: diff_basis_chi(:,:,:), basis(:,:,:)
    real(kind=r_def), pointer :: nodes(:,:) => null()

    ! Setup
    tol = 1.0e-6_r_def  
    qr = quadrature_type(3, GAUSSIAN)

    reference_element = QUAD
    call reference_cube()

    mesh = mesh_type(PLANE_BI_PERIODIC)

    ! Set element order

    element_order = 0


    ! Insert Psykal_lite unpacking code here
    do i = 1,3
      chi(i)   = field_type( vector_space = fs%get_instance( mesh, element_order, W0 ) )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3          
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do


    do i = 1,3
      phys_field(i) = field_type( vector_space = fs%get_instance( mesh, element_order, W1 ) )
      phys_p(i) = phys_field(i)%get_proxy()   
    end do
    comp_field =  field_type( vector_space = fs%get_instance( mesh, element_order, W1 ) )
    comp_p = comp_field%get_proxy()

    nlayers = comp_p%vspace%get_nlayers()

    ndf  = comp_p%vspace%get_ndf( )
    undf = comp_p%vspace%get_undf()
    dim  = comp_p%vspace%get_dim_space( )
    allocate(basis(dim, ndf, ndf) )

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf()
    diff_dim_chi = chi_p(1)%vspace%get_dim_space_diff( )
    allocate(diff_basis_chi(diff_dim_chi, ndf_chi, ndf))

    nodes => comp_p%vspace%get_nodes( )
    call chi_p(1)%vspace%compute_nodal_diff_basis_function(diff_basis_chi, ndf_chi, ndf, nodes)    
    call comp_p%vspace%compute_nodal_basis_function(basis, ndf, ndf, nodes) 

    cell = 5
    map     => comp_p%vspace%get_cell_dofmap( cell )
    map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
    phys_p(1)%data(:) = 0.0_r_def
    phys_p(2)%data(:) = 0.0_r_def
    phys_p(3)%data(:) = 0.0_r_def
    comp_p%data(:) = 0.0_r_def
    comp_p%data(map(1)) = 1.0_r_def
    comp_p%data(map(2)) = 3.0_r_def
    comp_p%data(map(6)) = -6.0_r_def

    call convert_hcurl_field_code(nlayers, &
                                  phys_p(1)%data, &
                                  phys_p(2)%data, &
                                  phys_p(3)%data, &
                                  comp_p%data, &
                                  chi_p(1)%data, &
                                  chi_p(2)%data, &
                                  chi_p(3)%data, &
                                  ndf, undf, map, &
                                  ndf_chi, undf_chi, map_chi, &
                                  basis, &
                                  diff_basis_chi &
                                 )
    deallocate(diff_basis_chi, basis)


    answer = 1.0_r_def / dy
    @assertEqual(answer, phys_p(2)%data(map(1)), tol)
    answer = 3.0_r_def / dx
    @assertEqual(answer, phys_p(1)%data(map(2)), tol)
    answer = -6.0_r_def / dz
    @assertEqual(answer, phys_p(3)%data(map(6)), tol)

    call deallocate_reference()
    call purge_function_spaces()

  
  end subroutine convert_hcurl_field_kernel_test

end module convert_hcurl_field_kernel_mod_test
