!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the W2 mass matrix computation
!>
module compute_mass_matrix_kernel_w2_mod_test

  implicit none 


contains

  @test
  subroutine compute_mass_matrix_kernel_w2_test()

    use pFUnit_Mod
    use field_mod,                  only : field_type,      &
                                           field_proxy_type
    use compute_mass_matrix_kernel_w2_mod, &
                                    only : compute_mass_matrix_w2_code
    use constants_mod,              only : i_def, r_def
    use quadrature_mod,             only : quadrature_type, GAUSSIAN
    use operator_mod,               only : operator_type, operator_proxy_type

    use configuration_mod,     only: QUAD, element_order
    use mesh_mod,              only: mesh_type, PLANE_BI_PERIODIC
    use function_space_mod,    only: function_space_type                       &
                                   , purge_function_spaces
    use fs_continuity_mod,     only: W0, W2
    use reference_element_mod, only: reference_cube, deallocate_reference      &
                                   , reference_element

    implicit none

    type(field_type) :: chi(3)
    type(field_proxy_type )   :: chi_p(3) 

    type(mesh_type)           :: mesh
    type(quadrature_type)     :: qr
    type(function_space_type) :: fs
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()




    real(kind=r_def) :: tol
    real(kind=r_def) :: answer(6,6)

    integer :: i, j, k, cell
    integer(i_def) :: err

    real(kind=r_def), parameter :: dx = 6000.0_r_def, &
                                   dy = 1000.0_r_def

    real(r_def) :: dz, dz_test(3)

    ! Required for calling mass matrix kernel    
    type( operator_type ) :: mm

    integer :: nlayers, ndf, dim, diff_dim
    integer :: ndf_chi, undf_chi
    integer :: nqp_h, nqp_v

    integer, pointer        ::  map_chi(:)  => null()
    real ( kind=r_def ), allocatable :: diff_basis(:,:,:,:), &
                                        basis(:,:,:,:)
    type( operator_proxy_type) :: mm_p

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    tol = 1.0e-6_r_def
    qr  = quadrature_type(3,GAUSSIAN)

    reference_element = QUAD
    call reference_cube()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh = mesh_type(PLANE_BI_PERIODIC)

    call mesh%get_dz(dz_test)
    dz = dz_test(1)

    w0_fs => fs%get_instance( mesh, element_order, W0 )
    w2_fs => fs%get_instance( mesh, element_order, W2 )

    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i) = field_type( vector_space = w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3          
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test mass matrix kernel
    mm = operator_type(w2_fs,w2_fs)
    mm_p = mm%get_proxy()  
    
    nqp_h=qr%get_nqp_h()
    nqp_v=qr%get_nqp_v()
    zp=>qr%get_xqp_v()
    xp=>qr%get_xqp_h()
    wh=>qr%get_wqp_h()
    wv=>qr%get_wqp_v()

    ndf_chi      = chi_p(1)%vspace%get_ndf()
    undf_chi     = chi_p(1)%vspace%get_undf()
    diff_dim = chi_p(1)%vspace%get_dim_space_diff( )
    allocate(diff_basis(diff_dim,ndf_chi,nqp_h,nqp_v) )

    nlayers  = mm_p%fs_from%get_nlayers()
    ndf  = mm_p%fs_from%get_ndf()
    dim = mm_p%fs_from%get_dim_space( )
    allocate(basis(dim,ndf,nqp_h,nqp_v) )

    call chi_p(1)%vspace%compute_diff_basis_function(                    &
         diff_basis,ndf_chi, nqp_h, nqp_v, xp, zp )

    call mm_p%fs_from%compute_basis_function(                            &
         basis, ndf, nqp_h, nqp_v, xp, zp ) 

    cell = 1
    map_chi => chi_p(1)%vspace%get_cell_dofmap(cell)

    mm_p%local_stencil(:,:,:) = 0.0_r_def
    call compute_mass_matrix_w2_code( cell,                              &
                                      nlayers,                           &
                                      mm_p%ncell_3d,                     &
                                      mm_p%local_stencil,                &
                                      chi_p(1)%data,                     &
                                      chi_p(2)%data,                     &
                                      chi_p(3)%data,                     &
                                      ndf,                               &
                                      basis,                             &
                                      ndf_chi,                           &
                                      undf_chi,                          &
                                      map_chi,                           &
                                      diff_basis,                        &
                                      nqp_h,                             &
                                      nqp_v,                             &
                                      wh,                                &
                                      wv                                 &
                                      )
    
    deallocate(basis, diff_basis)

    call mass_matrix_w2(answer)
    ! Normalise by 1/average edge length
    answer(:,:) = answer(:,:) * ( dx + dy + dz ) / 3.0_r_def
    mm_p%local_stencil(:, :, 1 ) = mm_p%local_stencil(:, :, 1 ) &
                                * ( dx + dy + dz ) / 3.0_r_def

    @assertEqual(answer, mm_p%local_stencil(:, :, 1 ), tol)

    ! Tear down ...
    call deallocate_reference()
    call purge_function_spaces()

  end subroutine compute_mass_matrix_kernel_w2_test

  subroutine mass_matrix_w2( mm )
    use constants_mod, only: r_def  
    implicit none

    real(kind=r_def), dimension(6,6), intent(inout) :: mm
    mm = reshape( [ &
  0.9999999999999905E-03_r_def, &
  0.0000000000000000E+00_r_def, &
  0.4999999999999963E-03_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.2777777777777752E-04_r_def, &
  0.0000000000000000E+00_r_def, &
  0.1388888888888879E-04_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.4999999999999963E-03_r_def, &
  0.0000000000000000E+00_r_def, &
  0.9999999999999907E-03_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.1388888888888879E-04_r_def, &
  0.0000000000000000E+00_r_def, &
  0.2777777777777752E-04_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.1111111111111101E-03_r_def, &
  0.5555555555555515E-04_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.0000000000000000E+00_r_def, &
  0.5555555555555515E-04_r_def, &
  0.1111111111111101E-03_r_def  &
    ], shape(mm) )
    return
  end subroutine mass_matrix_w2

end module compute_mass_matrix_kernel_w2_mod_test
