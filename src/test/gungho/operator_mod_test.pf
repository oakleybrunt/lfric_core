!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the Operator representation
!>

module operator_mod_test
  implicit none

  contains

    @test
    subroutine operator_test()

      use pFUnit_Mod
      use constants_mod,         only: i_def, r_def
      use operator_mod,          only: operator_type                           &
                                     , operator_proxy_type
      use configuration_mod,     only: QUAD, element_order
      use mesh_mod,              only: mesh_type, PLANE_BI_PERIODIC
      use function_space_mod,    only: function_space_type                     &
                                     , purge_function_spaces
      use fs_continuity_mod,     only: W1, W2
      use reference_element_mod, only: reference_cube, deallocate_reference    &
                                     , reference_element

      implicit none

      type( operator_type )       :: Op
      type( operator_proxy_type ) :: Op_p

      type(mesh_type)             :: mesh
      type( function_space_type ) :: fs
      type( function_space_type ), pointer :: fs1  => null()
      type( function_space_type ), pointer :: fs2  => null()


      integer(i_def) :: err
      integer :: ncell_3d_1, ncell_3d_2, ls_size

      reference_element = QUAD
      call reference_cube()

      ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
      mesh = mesh_type(PLANE_BI_PERIODIC)    

      fs1 => fs%get_instance( mesh, element_order, W1 )
      fs2 => fs%get_instance( mesh, element_order, W2 )

      ! make an operator
      Op = operator_type( fs2, fs1 )  ! fs1 --> fs2

      ! get the proxy
      Op_p = Op%get_proxy()

      ! Check we get back what we sowed
      @assertEqual( Op%which_fs_from(), W1 )
      @assertEqual( Op%which_fs_to(),   W2 )

      ! Only meaningful test without PSy and Kernel layers is to check array 
      ! sizes on the proxy.
      ncell_3d_1 = Op_p%fs_from%get_ncell() * Op_p%fs_from%get_nlayers() 
      ncell_3d_2 = Op_p%fs_to%get_ncell() * Op_p%fs_to%get_nlayers() 
      @assertEqual( Op_p%ncell_3d, ncell_3d_1 )
      @assertEqual( Op_p%ncell_3d, ncell_3d_2 )
      ls_size = ncell_3d_1 * Op_p%fs_from%get_ndf() * Op_p%fs_to%get_ndf() 
      @assertEqual( size( Op_p%local_stencil ), ls_size )

      ! Tear down
      call deallocate_reference()
      call purge_function_spaces()

    end  subroutine operator_test
end module operator_mod_test
