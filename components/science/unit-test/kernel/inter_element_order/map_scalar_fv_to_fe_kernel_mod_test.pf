!-----------------------------------------------------------------------------
! (C) Crown copyright 2025 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> Test the mapping of a lower order space to a higher order space on a finer
!! mesh.
module map_scalar_fv_to_fe_kernel_mod_test

  use, intrinsic :: iso_fortran_env,     only: real64
  use funit
  use constants_mod,                     only: i_def, r_def
  use inter_element_order_test_data_mod, only: w3_fv_to_fe_weights, &
                                               w3_fe

  implicit none

contains

  @test
  subroutine test_all
    use sci_map_scalar_fv_to_fe_kernel_mod, only: map_scalar_fv_to_fe_code

    implicit none

    ! Scalars
    integer(kind=i_def) :: lp_x, lp_y, df, cell, k
    integer(kind=i_def) :: nlayers
    integer(kind=i_def) :: ncell_f_per_c_x ! Number of fine cells per coarse in x
    integer(kind=i_def) :: ncell_f_per_c_y ! Number of fine cells per coarse in y
    integer(kind=i_def) :: ncell_f         ! Number of fine cells
    integer(kind=i_def) :: ndf_f           ! Number of dofs per fine cell
    integer(kind=i_def) :: ndf_c           ! Number of dofs per coarse cell
    integer(kind=i_def) :: undf_f, undf_c  ! Number of dofs on the mesh
    integer(kind=i_def) :: undf_weights    ! Number of dofs on the weights field
    integer(kind=i_def) :: ndata           ! Number of data points per dof in weights field
    real(kind=r_def)    :: use_tol

    ! Integer Arrays
    integer(kind=i_def), allocatable, dimension(:,:) :: cell_map
    integer(kind=i_def), allocatable, dimension(:,:) :: map_f ! Cell and dof indexed
    integer(kind=i_def), allocatable, dimension(:)   :: map_c ! Only dof indexed
    integer(kind=i_def), allocatable, dimension(:)   :: map_weights

    ! Real Arrays
    real(kind=r_def), allocatable, dimension(:) :: coarse_field, &
                                                   fine_field

    ! Pointers
    real(kind=r_def), pointer                   :: weights(:)
    real(kind=r_def), pointer                   :: answer(:)

    ! Parameters
    real(r_def), parameter :: tol = 1.0e-12_r_def

    if (r_def == real64) then
      use_tol = tol
    else
      use_tol = 10.0_r_def*spacing(1.0_r_def)
    end if

    ! Set scalars
    nlayers = 3
    ncell_f_per_c_x = 2
    ncell_f_per_c_y = 2
    ndf_f = 1 ! W3, discontinuous
    ncell_f = ndf_f * ncell_f_per_c_x * ncell_f_per_c_y
    ndf_c = 4 ! Linear in horizontal, constant in vertical
    undf_f = nlayers * ncell_f * ndf_f
    undf_c = nlayers * ndf_c
    ndata = 4
    undf_weights = ndf_c*ncell_f

    allocate( cell_map(ncell_f_per_c_x,  &
                       ncell_f_per_c_y), &
              map_f(ndf_f,ncell_f),      &
              map_c(ndf_c),              &
              map_weights(ndf_c) )
    allocate( coarse_field(undf_c),  &
              fine_field(undf_f) )

    ! Set simple values for index sets
    do lp_y = 1, ncell_f_per_c_y
      do lp_x = 1, ncell_f_per_c_x
        cell_map(lp_x, lp_y) = lp_x + (lp_y - 1)*ncell_f_per_c_y
      end do
    end do
    do cell = 1, ncell_f
       map_f(1,cell) = 1 + (cell-1)*nlayers
    end do
    do df = 1, ndf_c
      map_c(df) = 1 + (df-1)*nlayers
    end do
    do df = 1, ndf_c
      map_weights(df) = 1 + ndata*(df-1)
    end do

    ! Set simple values for the fields
    do k = 0, nlayers - 1
      do lp_y = 1, ncell_f_per_c_y
        do lp_x = 1, ncell_f_per_c_x
          fine_field(map_f(1, cell_map(lp_x, lp_y)) + k) = &
            real(cell_map(lp_x, lp_y) + 10*k, r_def)
        end do
      end do
    end do
    coarse_field(:) = 0.0_r_def

    ! Get weights
    weights => w3_fv_to_fe_weights

    ! Test mapping
    answer => w3_fe

    call map_scalar_fv_to_fe_code(               &
                               nlayers,          &
                               cell_map,         &
                               ncell_f_per_c_x,  &
                               ncell_f_per_c_y,  &
                               ncell_f,          &
                               coarse_field,     &
                               fine_field,       &
                               weights,          &
                               ndf_c,            &
                               undf_c,           &
                               map_c,            &
                               ndf_f,            &
                               undf_f,           &
                               map_f,            &
                               undf_weights,     &
                               map_weights)

    @assertEqual( answer, coarse_field, use_tol)


    deallocate( cell_map,     &
                map_f,        &
                map_c,        &
                map_weights,  &
                coarse_field, &
                fine_field )

    nullify( weights, &
             answer )

  end subroutine test_all

end module map_scalar_fv_to_fe_kernel_mod_test
