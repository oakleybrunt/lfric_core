!-----------------------------------------------------------------------------
! (C) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief  Simple algorithm to update the model data fields with each timestep
!>
module io_dev_timestep_alg_mod

  use constants_mod,                   only : r_def
  use clock_mod,                       only : clock_type
  use field_mod,                       only : field_type
  use field_parent_mod,                only : field_parent_type
  use field_collection_iterator_mod,   only : field_collection_iterator_type
  use field_collection_mod,            only : field_collection_type

  implicit none

  private
  public :: io_dev_timestep_alg

contains

!> @details An algorithm for updating fields with a time-based scaling factor
!>
!> @param[inout] depository The core model field collection
!> @param[in]    clock       The model clock object
!>
subroutine io_dev_timestep_alg(depository, clock)

    implicit none

    type(field_collection_type), intent(inout) :: depository
    class(clock_type),           intent(in)    :: clock

    type(field_collection_iterator_type) :: iter
    real(r_def)                          :: ts_scale_factor

    class(field_parent_type), pointer :: fld => null()
    type(field_type),         pointer :: fld_actual => null()

    ! Set up scaling factor for field using clock
    ts_scale_factor = 1.0_r_def / sqrt( real(clock%get_seconds_per_step(), r_def) )

    call iter%initialise(depository)
    do
      if ( .not.iter%has_next() ) exit
      fld => iter%next()
      select type(fld)
        type is (field_type)

          ! Apply scaling factor to field
          fld_actual => fld
          call invoke ( inc_a_times_x(ts_scale_factor, fld_actual) )

      end select
    end do

  end subroutine io_dev_timestep_alg

end module io_dev_timestep_alg_mod
