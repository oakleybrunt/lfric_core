!-----------------------------------------------------------------------------
! (c) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief   Algorithm for calculating indices used with COSMIC splitting to
!!          identify an orientation change due to cubed sphere panel edges.
!> @details A transport scheme with COSMIC splitting (e.g. FFSL) requires
!!          the field previously transported in the x and y directions.
!!          At cubed sphere panel edges the orientation may change, and so
!!          the field transported in the other direction may be needed.
!!          This algorithm finds the start and end indices for a stencil where
!!          the other direction field must be used.

module cosmic_index_alg_mod

  ! Constants and configs
  use constants_mod,                      only: i_def
  use field_mod,                          only: field_type
  use integer_field_mod,                  only: integer_field_type
  use geometric_constants_mod,            only: get_panel_id

  ! Kernels
  use cosmic_index_kernel_mod, only: cosmic_index_kernel_type

  implicit none

  private
  public :: cosmic_index_alg

contains

  !===========================================================================!
  !> @brief   Computes indices for orientation change due to cubed sphere panel edges.
  !> @details Computes start and end indices for when the orientation changes in a stencil
  !!          due to the change in cubed sphere panel so that the correct field can
  !!          be picked up for the FFSL transport scheme. The indices are based on
  !!          the stencil used in the FFSL flux kernels, i.e. they have the form
  !!          | 1 | 2 | 3 | 4 | 5 | for stencil extent 2.
  !!
  !> @param[in,out] index_start_x        Starting index for orientation change in stencil
  !!                                     for a field advected in the x direction
  !> @param[in,out] index_end_x          End index for orientation change in stencil
  !!                                     for a field advected in the x direction
  !> @param[in,out] index_start_y        Starting index for orientation change in stencil
  !!                                     for a field advected in the y direction
  !> @param[in,out] index_end_y          End index for orientation change in stencil
  !!                                     for a field advected in the y direction
  !> @param[in]     full_stencil_ext_x   The full stencil extent in x
  !> @param[in]     full_stencil_ext_y   The full stencil extent in y
  subroutine cosmic_index_alg( index_start_x,        &
                               index_end_x,          &
                               index_start_y,        &
                               index_end_y,          &
                               full_stencil_ext_x,   &
                               full_stencil_ext_y )

    implicit none

    ! Arguments
    type( integer_field_type ), intent(inout) :: index_start_x
    type( integer_field_type ), intent(inout) :: index_end_x
    type( integer_field_type ), intent(inout) :: index_start_y
    type( integer_field_type ), intent(inout) :: index_end_y
    integer(kind=i_def),        intent(in)    :: full_stencil_ext_x
    integer(kind=i_def),        intent(in)    :: full_stencil_ext_y

    ! Panel ID pointers
    type(field_type), pointer :: panel_id_x => null()
    type(field_type), pointer :: panel_id_y => null()

    ! Panel ID is required for both x and y stencils
    panel_id_x => get_panel_id(index_start_x%get_mesh_id())
    panel_id_y => get_panel_id(index_start_x%get_mesh_id())

    call invoke( cosmic_index_kernel_type( index_start_x,      &
                                           index_end_x,        &
                                           index_start_y,      &
                                           index_end_y,        &
                                           panel_id_x,         &
                                           full_stencil_ext_x, &
                                           panel_id_y,         &
                                           full_stencil_ext_y) )

  end subroutine cosmic_index_alg

end module cosmic_index_alg_mod
