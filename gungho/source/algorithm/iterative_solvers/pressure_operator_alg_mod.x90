!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------------
!>@brief Application of the Helmholtz operator
!>
!>@details The Helmholtz operator is given by
!>
!>   \f[ H =  M_3^{\Pi} + \tau_u\tau_\rho\Delta t*2 M_3^{\rho}M_3^{-1} D\left(\rho^*\mathbf{X}\right)
!>                      + \tau_u\tau_\theta\Delta t*2 P_{3\theta}\tilde{M}_\theta^{-1} P_{\theta2}\mathbf{X}\right) \f]
!>
!>
!> with the lumped matrix \f$H_B\f$ defined by
!>
!>   \f[ H_B=\tilde{M}_2 + M_\mu + \tau_u\tau_\theta(\Delta t)^2 \tilde{P_{2\theta}\tilde{M}_\theta^{-1}P_{theta2}\f]

!>
!> This module provides a type derived from the abstract operator type which
!> allows application of this Helmholtz operator.

module pressure_operator_alg_mod
  use linear_operator_mod,                only: abstract_linear_operator_type
  use vector_mod,                         only: abstract_vector_type
  use field_mod,                          only: field_type
  use field_vector_mod,                   only: field_vector_type
  use constants_mod,                      only: i_def, r_def
  use log_mod,                            only: log_event,       &
                                                LOG_LEVEL_ERROR, &
                                                LOG_LEVEL_INFO,  &
                                                log_scratch_space
  use runtime_constants_mod,              only: get_mass_matrix, &
                                                get_normalisation, &
                                                w2_id, wt_id, w3inv_id, &
                                                get_mask
  use fs_continuity_mod,                  only: W2, W3, Wtheta
  use si_operators_alg_mod,               only: get_m3_exner_star, &
                                                get_compound_div, &
                                                get_div_star, &
                                                get_ptheta2v, &
                                                get_p3theta, &
                                                get_helm_diag, &
                                                get_hb_lumped_inv
  use boundaries_config_mod,              only: limited_area
  implicit none

  type, public, extends(abstract_linear_operator_type) :: &
                        pressure_operator_type
  private
     !> Temporary fields used in operator application
     type(field_type) :: grad_p, div_grad_p

     contains
     !> over-ride the abstract interface
     !> param[in] self a linear operator
     !> param[in] x a fieldvector the linear operator is applied to
     !> param[inout] y a fieldvector, the result.
     procedure, public  :: apply => apply_pressure_operator
     procedure, private :: apply_pressure_operator
     !> performs a deep copy (for overriding the "=" operator)
     !> @param[out] dest   pressure_operator_type lhs
     !> @param[in]  source pressure_operator_type rhs
     procedure, public  :: pressure_operator_assign

     final              :: destroy_pressure_operator

     !> Override default assignment for pressure_operator_type pairs.
     generic            :: assignment(=) => pressure_operator_assign

  end type pressure_operator_type

  interface pressure_operator_type
     module procedure pressure_operator_constructor
  end interface

contains

  !>@brief Construct new instance of type
  !>
  !>@details Extract function spaces and build the lumped approximate
  !>         inverse \f$ \tilde{H}_b^{-1} \f$ as well as other operators
  !>         such as div and grad for the Helmholtz operator application.
  !>
  !>@param[in] state Field vector \f$(\textbf{U},P,B)\f$ representing
  !>                 current state. This is required for extracting the
  !>                 relevant function spaces required for operator
  !>                 construction
  !>@return instance of the Helmholtz operator
  function pressure_operator_constructor(state) result(self)
    use function_space_mod, only: function_space_type
    use field_indices_mod,  only: igh_u, igh_p
    use fs_continuity_mod,  only: W2
    use function_space_collection_mod, only: function_space_collection
    implicit none

    type(pressure_operator_type)        :: self
    type(field_vector_type), intent(in) :: state
    type(function_space_type), pointer  :: wp => null(), &
                                           wu => null()
    integer(i_def)                      :: p, mesh_id

    call log_event( 'Constructing pressure operator...', LOG_LEVEL_INFO )

    ! Get needed function spaces
    wp => state%vector(igh_p)%get_function_space()

    p = state%vector(igh_u)%get_element_order()
    mesh_id = state%vector(igh_u)%get_mesh_id()
    wu => function_space_collection%get_fs( mesh_id, p, W2 )

    ! Temporaries required in operator application
    call self%grad_p%initialise(vector_space = wu, name='grad_p')
    call self%div_grad_p%initialise(vector_space = wp, name='div_grad_p')

    call log_event( 'done', LOG_LEVEL_INFO )

  end function pressure_operator_constructor

  !> Performs a deep copy between pressure_operator_type pairs.
  !> (for overriding the "=" operator)
  !>
  !> @param[out] dest   pressure_operator_type lhs
  !> @param[in]  source pressure_operator_type rhs
  subroutine pressure_operator_assign(dest, source)

    implicit none
    class(pressure_operator_type), intent(in)  :: source
    class(pressure_operator_type), intent(out) :: dest

    ! Deep copy of the contents of the pressure_operator_type

    call source%grad_p%copy_field(dest%grad_p)
    call source%div_grad_p%copy_field(dest%div_grad_p)

  end subroutine pressure_operator_assign

  !>@brief Apply Helmholtz operator to a pressure field to obtain \f$y=Hx\f$
  !>
  !>@param[in] self instance of pressure_operator_type
  !>@param[in] x field \f$x\f$ to apply operator to
  !>@param[inout] y Resulting field \f$y=Hx\f$ in pressure space
  subroutine apply_pressure_operator(self, x, y)
    use scaled_matrix_vector_kernel_mod, only: scaled_matrix_vector_kernel_type, &
                                               opt_scaled_matrix_vector_kernel_type
    use operator_mod,                    only: operator_type
    use enforce_bc_kernel_mod,           only: enforce_bc_kernel_type
    use apply_variable_hx_kernel_mod,    only: apply_variable_hx_kernel_type, &
                                               opt_apply_variable_hx_kernel_type
    use dg_matrix_vector_kernel_mod,     only: dg_matrix_vector_kernel_type
    use finite_element_config_mod,       only: element_order
    use io_config_mod,                   only: subroutine_timers
    use timer_mod,                       only: timer
    use helmholtz_solver_config_mod,     only: normalise
    implicit none
    class(pressure_operator_type), intent(inout) :: self
    class(abstract_vector_type),   intent(in)    :: x
    class(abstract_vector_type),   intent(inout) :: y

    type(operator_type), pointer :: m3_exner_star => null(),   &
                                    div_star => null(),        &
                                    compound_div => null(),    &
                                    ptheta2 => null(),         &
                                    p3theta => null(),         &
                                    m3_inv => null()
    real(kind=r_def), parameter  :: one = 1.0_r_def
    type(field_type),    pointer :: mt_lumped_inv => null(),   &
                                    u_normalisation => null(), &
                                    h_diag => null(),          &
                                    hb_lumped_inv => null()
    type(field_type), pointer    :: w3_mask => null(), &
                                    w2_mask => null()

    if ( subroutine_timers ) call timer('helmholtz lhs')

    select type (x)
    type is (field_vector_type)
       select type (y)
       type is (field_vector_type)

         if ( limited_area ) then
           w3_mask       => get_mask(W3)
           w2_mask       => get_mask(W2)
         end if
         m3_exner_star   => get_m3_exner_star()
         div_star        => get_div_star()
         compound_div    => get_compound_div()
         ptheta2         => get_ptheta2v()
         mt_lumped_inv   => get_normalisation(wt_id)
         p3theta         => get_p3theta()
         u_normalisation => get_normalisation(w2_id)
         hb_lumped_inv   => get_hb_lumped_inv()
         ! Compute M3*p + H[ D{\theta*}*p ]
         if ( limited_area ) then
          if ( element_order == 0 ) then
             ! Hard-wired optimisation for desired configuration
             call invoke( setval_c( self%grad_p, 0.0_r_def ),                              &
                          opt_scaled_matrix_vector_kernel_type(self%grad_p, x%vector(1),   &
                                                               div_star, hb_lumped_inv,    &
                                                               u_normalisation),           &
                          inc_X_times_Y( self%grad_p, w2_mask ),                           &
                          opt_apply_variable_hx_kernel_type(self%div_grad_p, self%grad_p,  &
                                                            mt_lumped_inv, x%vector(1),    &
                                                            compound_div, p3theta, ptheta2,&
                                                            m3_exner_star, one) )
           else
             call invoke( setval_c( self%grad_p, 0.0_r_def ),                              &
                          scaled_matrix_vector_kernel_type(self%grad_p, x%vector(1),       &
                                                           div_star, hb_lumped_inv,        &
                                                           u_normalisation),               &
                          enforce_bc_kernel_type( self%grad_p ),                           &
                          inc_X_times_Y( self%grad_p, w2_mask ),                           &
                          apply_variable_hx_kernel_type(self%div_grad_p, self%grad_p,      &
                                                        mt_lumped_inv, x%vector(1),        &
                                                        compound_div, p3theta, ptheta2,    &
                                                        m3_exner_star, one) )

           end if
         else
           if ( element_order == 0 ) then
             ! Hard wired optimisation for desired configuration
             call invoke( setval_c( self%grad_p, 0.0_r_def ),                              &
                          opt_scaled_matrix_vector_kernel_type(self%grad_p, x%vector(1),   &
                                                               div_star, hb_lumped_inv,    &
                                                               u_normalisation),           &
                          opt_apply_variable_hx_kernel_type(self%div_grad_p, self%grad_p,  &
                                                            mt_lumped_inv, x%vector(1),    &
                                                            compound_div, p3theta, ptheta2,&
                                                            m3_exner_star, one) )
           else
             call invoke( setval_c( self%grad_p, 0.0_r_def ),                              &
                          scaled_matrix_vector_kernel_type(self%grad_p, x%vector(1),       &
                                                           div_star, hb_lumped_inv,        &
                                                           u_normalisation),               &
                          enforce_bc_kernel_type( self%grad_p ),                           &
                          apply_variable_hx_kernel_type(self%div_grad_p, self%grad_p,      &
                                                        mt_lumped_inv, x%vector(1),        &
                                                        compound_div, p3theta, ptheta2,    &
                                                        m3_exner_star, one) )

           end if
         end if

         m3_inv => get_mass_matrix(w3inv_id)
         call invoke( dg_matrix_vector_kernel_type(y%vector(1), self%div_grad_p, m3_inv) )

         if ( normalise ) then
           h_diag => get_helm_diag()
           call invoke( inc_X_times_Y(y%vector(1), h_diag) )
         end if

         if ( limited_area ) then
           call invoke( inc_X_times_Y( y%vector(1), w3_mask ) )
           nullify( w3_mask, w2_mask )
         end if

         nullify( m3_exner_star, div_star, compound_div, ptheta2,  &
                  p3theta, m3_inv, mt_lumped_inv, u_normalisation, &
                  h_diag )


    if ( subroutine_timers ) call timer('helmholtz lhs')

       class default
          write(log_scratch_space, '(A)') &
               "pressure_operator_alg_mod: incorrect vector_type argument y"
          call log_event(log_scratch_space, LOG_LEVEL_ERROR)
       end select
    class default
       write(log_scratch_space, '(A)') &
             "pressure_operator_alg_mod: incorrect vector_type argument x"
       call log_event(log_scratch_space, LOG_LEVEL_ERROR)
    end select

  end subroutine apply_pressure_operator

  !> Finalizer for the Helmholtz operator
  !> @param [inout] self the pressure operator
  subroutine destroy_pressure_operator(self)
    implicit none
    type(pressure_operator_type), intent(inout) :: self
  end subroutine destroy_pressure_operator

end module pressure_operator_alg_mod
