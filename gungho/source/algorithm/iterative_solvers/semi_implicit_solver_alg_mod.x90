!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief Semi-Implicit solver for the gungho dynamical core
module semi_implicit_solver_alg_mod

  use constants_mod,                      only: i_def,r_def
  use log_mod,                            only: log_event,         &
                                                log_scratch_space, &
                                                LOG_LEVEL_INFO,    &
                                                LOG_LEVEL_ERROR,   &
                                                LOG_LEVEL_TRACE


  ! Derived Types
  use field_vector_mod,                   only: field_vector_type
  use derived_config_mod,                 only: bundle_size

  ! Algorithms
  use mixed_operator_alg_mod,             only: mixed_operator_type
  use mixed_schur_preconditioner_alg_mod, only: mixed_schur_preconditioner_type
  use pressure_operator_alg_mod,          only: pressure_operator_type
  use pressure_precon_alg_mod,            only: pressure_preconditioner_type
  use pressure_diag_precon_alg_mod,       only: pressure_diag_preconditioner_type
  use null_preconditioner_alg_mod,        only: null_preconditioner_type

  ! preconditioner and solver
  use preconditioner_mod,                 only: abstract_preconditioner_type
  use iterative_solver_mod,               only: abstract_iterative_solver_type, &
                                                bicgstab_type,                  &
                                                gmres_type,                     &
                                                fgmres_type,                    &
                                                gcr_type,                       &
                                                conjugate_gradient_type,        &
                                                precondition_only_type

  use output_config_mod,                  only: subroutine_timers
  use timer_mod,                          only: timer

  implicit none

  private

  ! Operator, preconditioner and iterative solver for mixed problem
  type(mixed_operator_type)                          :: mixed_operator
  class(abstract_preconditioner_type),   allocatable :: mixed_preconditioner
  class(abstract_iterative_solver_type), allocatable :: mixed_solver
  
  !> Absolute solver tolerance (mixed solver)
  real(kind=r_def), parameter :: mixed_solver_a_tol = 1.E-6

  !> Operator, preconditioner and iterative solver for
  !> Helmholtz (pressure) problem
  type(pressure_operator_type)                       :: pressure_operator
  class(abstract_preconditioner_type),   allocatable :: pressure_preconditioner
  class(abstract_iterative_solver_type), allocatable :: pressure_solver
  
  !> Absolute solver tolerance (pressure solver)
  real(kind=r_def), parameter :: pressure_solver_a_tol = 1.E-8

  public :: semi_implicit_solver_alg_init
  public :: semi_implicit_solver_alg_final
  public :: semi_implicit_solver_alg_step

contains
!=============================================================================!
  !> @details Initialisation procedure for the semi-implicit solver 
  !> @param[in] state Prognostic state for the solver
  subroutine semi_implicit_solver_alg_init(state)
    use mixed_solver_config_mod,       only: si_maximum_iterations,                      &
                                             si_tolerance,                               &
                                             si_method,                                  &
                                             mixed_gcrk => gcrk,                         &
                                             mixed_solver_si_method_cg,                  &
                                             mixed_solver_si_method_bicgstab,            &
                                             mixed_solver_si_method_gmres,               &
                                             mixed_solver_si_method_fgmres,              &
                                             mixed_solver_si_method_gcr,                 &
                                             mixed_solver_si_method_prec_only,           &
                                             si_preconditioner,                          &
                                             mixed_solver_si_preconditioner_pressure,    &
                                             mixed_solver_si_preconditioner_diagonal,    &
                                             mixed_solver_si_preconditioner_none

    use helmholtz_solver_config_mod,   only: si_pressure_maximum_iterations,             &
                                             helmholtz_gcrk => gcrk,                     &
                                             si_pressure_tolerance,                      &
                                             helmholtz_method => method,                 &
                                             helmholtz_solver_method_cg,                 &
                                             helmholtz_solver_method_bicgstab,           &
                                             helmholtz_solver_method_gmres,              &
                                             helmholtz_solver_method_fgmres,             &
                                             helmholtz_solver_method_gcr,                &
                                             helmholtz_solver_method_prec_only,          &
                                             helmholtz_preconditioner => preconditioner, &
                                             helmholtz_solver_preconditioner_none,       &
                                             helmholtz_solver_preconditioner_diagonal,   &
                                             helmholtz_solver_preconditioner_tridiagonal
    implicit none

    ! Prognostic fields    
    type( field_vector_type ), intent( inout ) :: state

    ! *** Allocate polymorphic solver- and preconditioner objects ***

    ! Allocate pressure solver of correct type
    select case( helmholtz_method )
    case (HELMHOLTZ_SOLVER_METHOD_BICGSTAB)
       allocate ( bicgstab_type :: pressure_solver )
    case(HELMHOLTZ_SOLVER_METHOD_CG)
       allocate ( conjugate_gradient_type :: pressure_solver )
    case(HELMHOLTZ_SOLVER_METHOD_GMRES)
       allocate ( gmres_type :: pressure_solver )
    case(HELMHOLTZ_SOLVER_METHOD_FGMRES)
       allocate ( fgmres_type :: pressure_solver )
    case(HELMHOLTZ_SOLVER_METHOD_GCR)
       allocate ( gcr_type :: pressure_solver )
    case(HELMHOLTZ_SOLVER_METHOD_PREC_ONLY)
       allocate ( precondition_only_type :: pressure_solver )

    case default
       call log_event("Unknown pressure solver specified",LOG_LEVEL_ERROR)
    end select

    ! Allocate pressure preconditioner preconditioner of correct type
    select case(helmholtz_preconditioner)
    case(HELMHOLTZ_SOLVER_PRECONDITIONER_NONE)
       allocate ( null_preconditioner_type :: pressure_preconditioner)
    case(HELMHOLTZ_SOLVER_PRECONDITIONER_DIAGONAL)
       allocate ( pressure_diag_preconditioner_type :: pressure_preconditioner)
    case(HELMHOLTZ_SOLVER_PRECONDITIONER_TRIDIAGONAL)
       allocate ( pressure_preconditioner_type :: pressure_preconditioner)
    case default
       call log_event( "Unknown pressure preconditioner specified", &
                       LOG_LEVEL_ERROR)
    end select

    ! Allocate mixed preconditioner of correct type
    select case(si_preconditioner)
    case(MIXED_SOLVER_SI_PRECONDITIONER_PRESSURE)
       allocate ( mixed_schur_preconditioner_type :: mixed_preconditioner)
    case(MIXED_SOLVER_SI_PRECONDITIONER_NONE)
       allocate ( null_preconditioner_type :: mixed_preconditioner)
    case default
       call log_event("Unknown mixed preconditioner specified",LOG_LEVEL_ERROR)
    end select

    ! Allocate mixed solver of correct type
    select case(si_method)
    case(MIXED_SOLVER_SI_METHOD_BICGSTAB)
      allocate ( bicgstab_type :: mixed_solver )
    case(MIXED_SOLVER_SI_METHOD_CG)
      allocate ( conjugate_gradient_type :: mixed_solver )
    case(MIXED_SOLVER_SI_METHOD_GMRES)
      allocate ( gmres_type :: mixed_solver )
    case(MIXED_SOLVER_SI_METHOD_FGMRES)
      allocate ( fgmres_type :: mixed_solver )
    case(MIXED_SOLVER_SI_METHOD_GCR)
      allocate ( gcr_type :: mixed_solver )
    case(MIXED_SOLVER_SI_METHOD_PREC_ONLY)
       allocate ( precondition_only_type :: mixed_solver )
    case default
       call log_event("Unknown mixed solver specified",LOG_LEVEL_ERROR)
    end select

    ! *** Construct solver-, preconditioner- and operator-objects

    ! Construct Helmholtz operator and preconditioner
    pressure_operator = pressure_operator_type(state)

    ! Construct pressure preconditioner
    select type(pressure_preconditioner)
    type is ( null_preconditioner_type )
       ! Null preconditioner
       pressure_preconditioner = null_preconditioner_type()
    type is ( pressure_preconditioner_type )
       ! Vertical preconditioner
       pressure_preconditioner = pressure_preconditioner_type()
    type is ( pressure_diag_preconditioner_type )
       ! Diagonal preconditioner
       pressure_preconditioner = pressure_diag_preconditioner_type()
    class default
       call log_event( "Can not initialise pressure preconditioner", &
                       LOG_LEVEL_ERROR )
    end select

    call log_event( "Setting up pressure solver", LOG_LEVEL_INFO )

    ! Construct pressure solver
    select type(pressure_solver)
    type is ( conjugate_gradient_type )        
       ! CG solver
       pressure_solver = conjugate_gradient_type( pressure_operator,           &
                                                  pressure_preconditioner,     &
                                                  si_pressure_tolerance,       &
                                                  pressure_solver_a_tol,       &
                                                  si_pressure_maximum_iterations)
    type is ( bicgstab_type )        
       ! BiCGStab solver
       pressure_solver = bicgstab_type( pressure_operator,           &
                                        pressure_preconditioner,     &
                                        si_pressure_tolerance,       &
                                        pressure_solver_a_tol,       &
                                        si_pressure_maximum_iterations)
    type is ( gmres_type )        
       ! GMRES solver
       pressure_solver = gmres_type( pressure_operator,           &
                                     pressure_preconditioner,     &
                                     helmholtz_gcrk,              &
                                     si_pressure_tolerance,       &
                                     pressure_solver_a_tol,       &
                                     si_pressure_maximum_iterations)
    type is ( fgmres_type )        
       ! fGMRES solver
       pressure_solver = fgmres_type( pressure_operator,           &
                                      pressure_preconditioner,     &
                                      helmholtz_gcrk,              &
                                      si_pressure_tolerance,       &
                                      pressure_solver_a_tol,       &
                                      si_pressure_maximum_iterations)
    type is ( gcr_type )        
       ! GCR solver
       pressure_solver = gcr_type( pressure_operator,           &
                                   pressure_preconditioner,     &
                                   helmholtz_gcrk,              &
                                   si_pressure_tolerance,       &
                                   pressure_solver_a_tol,       &
                                   si_pressure_maximum_iterations)
    type is ( precondition_only_type )        
       ! Precondition only solver
       pressure_solver = precondition_only_type( pressure_operator,           &
                                                 pressure_preconditioner,     &
                                                 si_pressure_tolerance,       &
                                                 pressure_solver_a_tol)

    class default
       call log_event("Can not initialise pressure solver",LOG_LEVEL_ERROR)
    end select

    call log_event( "Setting up mixed operator", LOG_LEVEL_INFO )
    ! Set up mixed operator
    mixed_operator = mixed_operator_type()
    
    call log_event( "Setting up mixed precon", LOG_LEVEL_INFO )
    ! Set up mixed preconditioner
    select type(mixed_preconditioner)
    type is ( mixed_schur_preconditioner_type )
       ! Schur-complement preconditioner
       mixed_preconditioner = &
           mixed_schur_preconditioner_type(state, pressure_solver)
    type is ( null_preconditioner_type )
       ! Null preconditioner
       mixed_preconditioner = null_preconditioner_type()
    class default
       call log_event("Can not initialise mixed preconditioner", &
                      LOG_LEVEL_ERROR)
    end select

    call log_event( "Setting up mixed solver", LOG_LEVEL_INFO )
    select type(mixed_solver) 
    type is ( conjugate_gradient_type )        
       mixed_solver = conjugate_gradient_type( mixed_operator,       &
                                               mixed_preconditioner, &
                                               si_tolerance,         &
                                               mixed_solver_a_tol,   &
                                               si_maximum_iterations)
    type is ( bicgstab_type )        
       mixed_solver = bicgstab_type( mixed_operator,       &
                                     mixed_preconditioner, &
                                     si_tolerance,         &
                                     mixed_solver_a_tol,   &
                                     si_maximum_iterations)
    type is ( gmres_type )        
       mixed_solver = gmres_type( mixed_operator,       &
                                  mixed_preconditioner, &
                                  mixed_gcrk,           &
                                  si_tolerance,         &
                                  mixed_solver_a_tol,   &
                                  si_maximum_iterations)
    type is ( fgmres_type )        
       mixed_solver = fgmres_type( mixed_operator,       &
                                   mixed_preconditioner, &
                                   mixed_gcrk,           &
                                   si_tolerance,         &
                                   mixed_solver_a_tol,   &
                                   si_maximum_iterations)
    type is ( gcr_type )        
       mixed_solver = gcr_type( mixed_operator,       &
                                mixed_preconditioner, &
                                mixed_gcrk,           &
                                si_tolerance,         &
                                mixed_solver_a_tol,   &
                                si_maximum_iterations)
    type is ( precondition_only_type )        
       mixed_solver = precondition_only_type( mixed_operator,       &
                                              mixed_preconditioner, &
                                              si_tolerance,         &
                                              mixed_solver_a_tol)
    class default
       call log_event("Can not initialise mixed solver",LOG_LEVEL_ERROR)
    end select

    call log_event( "Initialised semi-implicit solver", LOG_LEVEL_INFO )

  end subroutine semi_implicit_solver_alg_init

  !@brief Tidy up semi-implicit solver algorithm module
  !>
  !@details Deallocate memory
  subroutine semi_implicit_solver_alg_final()
    implicit none

    ! Deallocate mixed preconditioner object
    if (allocated(mixed_preconditioner)) then
       deallocate(mixed_preconditioner)
    end if
    ! Deallocate mixed solver object
    if (allocated(mixed_solver)) then
       deallocate(mixed_solver)
    end if
    ! Deallocate pressure preconditioner object
    if (allocated(pressure_preconditioner)) then
       deallocate(pressure_preconditioner)
    end if
    ! Deallocate pressure solver object
    if (allocated(pressure_solver)) then
       deallocate(pressure_solver)
    end if
  end subroutine semi_implicit_solver_alg_final

!=============================================================================!

  !> @details An algorithm for timestepping the semi-implicit equations
  !>
  !> @param[inout] state Prognostic model state
  !> @param[in]    rhs   Residuals
  subroutine semi_implicit_solver_alg_step(state, rhs)

    implicit none

    ! Prognostic fields    
    type( field_vector_type ), intent( inout ) :: state, rhs

    if ( subroutine_timers ) call timer('semi_implicit_solver_alg')
    call log_event( "Gungho: mixed solve:", LOG_LEVEL_INFO )
    call mixed_solver%apply(state, rhs)
    if ( subroutine_timers ) call timer('semi_implicit_solver_alg')

  end subroutine semi_implicit_solver_alg_step

end module semi_implicit_solver_alg_mod
