!-----------------------------------------------------------------------------
! (C) Crown copyright 2017 Met Office. All rights reserved.
! For further details please refer to the file LICENCE which you should have
! received as part of this distribution.
!-----------------------------------------------------------------------------
!> @brief An algorithm for COSMIC transport scheme on the cubed-sphere.
!> @details The algorithm iterates forwards in time and transports the density
!>          field using the COSMIC advection scheme. This implementation
!>          has been tested on the cubed-sphere domain and in
!>          the horizontal direction only.
module cusph_cosmic_transport_alg_mod

  use constants_mod,                     only: r_def, i_def
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type
  use function_space_collection_mod,     only: function_space_collection
  use finite_element_config_mod,         only: element_order
  use fs_continuity_mod,                 only: W2, W3
  use log_mod,                           only: log_event,         &
                                               log_scratch_space, &
                                               LOG_LEVEL_INFO,    &
                                               LOG_LEVEL_TRACE
  use runtime_constants_mod,             only: get_coordinates

  use cusph_split_transport_alg_mod,     only: cusph_split_transport_alg
  use cusph_fv_density_update_alg_mod,   only: cusph_fv_density_update_alg
  use flux_direction_mod,                only: x_direction, y_direction
  use initial_u_kernel_mod,              only: initial_u_kernel_type
  use quadrature_mod,                    only: quadrature_type, GAUSSIAN
  use timestepping_config_mod,           only: dt
  use solver_mod,                        only: solver_algorithm

  implicit none

  private

  ! 'State' items that need to be created once but used every step
  type( field_type ) :: u_n, u_np1, rho_n, rho_np1, mass_flux_x, mass_flux_y
  type( field_type ) :: rho_temp, r_u

  public :: cusph_cosmic_transport_init
  public :: cusph_cosmic_transport_step

contains

  !> @brief Init routine for cosmic transport timestepping algorithm.
  !> @details Rho and u fields are initialised before
  !>          this algorithm is called. State items are created.
  !> @param[in]      mesh_id   Mesh id of mesh on which the model runs
  !> @param[inout]   u         3D wind field
  !> @param[in]      timestep  Timestep
  subroutine cusph_cosmic_transport_init(mesh_id, u, timestep)

    implicit none

    integer(i_def),      intent(in)    :: mesh_id
    integer(i_def),      intent(in)    :: timestep

    ! Prognostic wind field
    type( field_type ), intent( inout ) :: u

    type(function_space_type), pointer :: w2_fs   => null()
    type(function_space_type), pointer :: w3_fs   => null()
    type( quadrature_type )            :: qr
    type(field_type),          pointer :: chi(:) => null()
    real(r_def)                        :: time

    w2_fs   => function_space_collection%get_fs( mesh_id, element_order, W2 )
    w3_fs   => function_space_collection%get_fs( mesh_id, element_order, W3 )

    rho_n       = field_type( vector_space = w3_fs )
    rho_np1     = field_type( vector_space = w3_fs )
    rho_temp    = field_type( vector_space = w3_fs )
    u_n         = field_type( vector_space = w2_fs )
    u_np1       = field_type( vector_space = w2_fs )
    mass_flux_x = field_type( vector_space = w2_fs )
    mass_flux_y = field_type( vector_space = w2_fs )
    r_u         = field_type( vector_space = w2_fs )

    qr = quadrature_type(element_order+2, GAUSSIAN)
    chi => get_coordinates()


    time = dt*real(timestep,r_def)
    call invoke( set_field_scalar(0.0_r_def, u), &
                 set_field_scalar(0.0_r_def, r_u) )

    call invoke( initial_u_kernel_type(r_u, chi, time, qr) )
    call solver_algorithm(u, r_u)

    call invoke( copy_field(u,u_n), &
                 copy_field(u,u_np1) )

  end subroutine cusph_cosmic_transport_init

  !> @brief An algorithm for testing the COSMIC transport scheme.
  !> @details The algorithm iterates forwards in time and transports the density
  !>          field using the COSMIC advection scheme. This implementation
  !>          has currently been tested in the biperiodic planar domain and in
  !>          the horizontal direction only. The wind profile is defined
  !>          analytically.
  !> @param[in]    mesh_id          Mesh id of the mesh all fields are on
  !> @param[inout] rho              Density field
  !> @param[in]    cell_orientation Orientation of cells, held in a W3 field
  !> @param[in]    detj_at_w2       Detj values at W2 dof locations held in a W2 field
  subroutine cusph_cosmic_transport_step( mesh_id,            &
                                          rho,                &
                                          cell_orientation,   &
                                          detj_at_w2 )

    implicit none

    integer(i_def),      intent(in)    :: mesh_id
    type(field_type),    intent(inout) :: rho
    type(field_type),    intent(in)    :: cell_orientation
    type(field_type),    intent(in)    :: detj_at_w2

    type(field_type), pointer :: chi(:) => null()

    chi => get_coordinates()

    call invoke( copy_field(rho,rho_n) )

    ! Call algorithm to calculate mass fluxes using COSMIC scheme
    call cusph_split_transport_alg( u_n, u_np1, rho_n, chi, cell_orientation,  &
                                 mesh_id, mass_flux_x, mass_flux_y, detj_at_w2 )

    call invoke(set_field_scalar(-99.0_r_def,rho_np1))
    call cusph_fv_density_update_alg(rho_n, mass_flux_x, mass_flux_y, rho_temp,     &
                                      cell_orientation, x_direction, detj_at_w2)

    call cusph_fv_density_update_alg(rho_temp, mass_flux_x, mass_flux_y, rho_np1,   &
                                      cell_orientation, y_direction, detj_at_w2)

    call invoke( copy_field(rho_np1,rho) )

  end subroutine cusph_cosmic_transport_step

end module cusph_cosmic_transport_alg_mod
