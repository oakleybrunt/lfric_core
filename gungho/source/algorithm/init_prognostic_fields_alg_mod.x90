!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!>@brief Initialisation of prognostic fields
module init_prognostic_fields_alg_mod

  use log_mod,                         only: log_event,         &
                                             log_scratch_space, &
                                             LOG_LEVEL_INFO,    &
                                             LOG_LEVEL_TRACE
  use constants_mod,                   only: r_def, i_def

  use solver_mod,                      only: solver_algorithm
  use runtime_constants_mod,           only: get_coordinates

  ! Configuration and restart/checkpoint options
  use finite_element_config_mod,       only: element_order
  use formulation_config_mod,          only: use_moisture
  use transport_config_mod,            only: scheme, &
                                             operators, &
                                             transport_scheme_method_of_lines, &
                                             transport_operators_fv
  use initial_density_config_mod,      only: sample_init_rho
  use restart_control_mod,             only: restart_type
  use timestepping_config_mod,         only: dt


  ! PsyKAl-lite kernels
  use psykal_lite_mod,                 only: invoke_initial_mr_kernel, &
                                             invoke_initial_theta_kernel, &
                                             invoke_initial_rho_sample_kernel

  ! PsyKAl PSYClone kernels
  use set_rho_kernel_mod,              only: set_rho_kernel_type
  use initial_u_kernel_mod,            only: initial_u_kernel_type

  ! Derived Types
  use function_space_collection_mod,   only: function_space_collection
  use function_space_mod,              only: function_space_type
  use field_mod,                       only: field_type
  use quadrature_mod,                  only: quadrature_type, GAUSSIAN
  use operator_mod,                    only: operator_type

  ! Handles
  use fs_continuity_mod,               only: W0, W1, W2, W3, Wtheta
  use mr_indices_mod,                  only: nummr
  use map_rho_to_theta_mod,            only: map_rho_to_theta
  implicit none

  private
  public :: init_prognostic_fields_alg

contains
  !> @details An algorithm for initialising prognostic fields for 
  !>          all solvers.
  !> @param[in] mesh_id Id of Mesh object on which the model runs
  !> @param[inout] u  The 3D wind field
  !> @param[inout] rho The density
  !> @param[inout] theta The potential temperature
  !> @param[inout] xi The vorticity field 
  !> @param[in] restart Checkpoint/restart type with timestepping information
  subroutine init_prognostic_fields_alg( mesh_id, u, rho, theta, rho_in_wth, mr, xi, restart)

    implicit none

    ! Mesh id
    integer(i_def),  intent(in)         :: mesh_id
    ! Prognostic fields
    type( field_type ), intent( inout ) :: u, rho, theta, xi, mr(nummr), rho_in_wth
    ! Control
    type( restart_type ), intent(in)    :: restart

    type( quadrature_type )             :: qr
    type( field_type )                  :: r_u
    ! Coordinate fields
    type( field_type ), pointer         :: chi(:) => null()


    integer :: i
    character(5) :: name

    real(kind=r_def) :: initial_time

    qr = quadrature_type( element_order + 3, GAUSSIAN )    
    chi => get_coordinates()

    !=== Initialise global prognostic fields ==================================!

    if (.not.restart%read_file()) then           ! No check point to start from

      call log_event( "Gungho: Initialising prognostic fields", LOG_LEVEL_INFO )

      ! Initialise Xi
      call invoke( set_field_scalar(0.0_r_def, xi) )
      ! Initialise theta
      call invoke_initial_theta_kernel( theta, chi )

      initial_time = 0.0_r_def

      ! Initialise U and Rho according to formulation
      r_u = field_type( vector_space =                                           &
                  function_space_collection%get_fs(mesh_id, element_order, W2) )
      call invoke( set_field_scalar(0.0_r_def, u), &
                   set_field_scalar(0.0_r_def, r_u) )
      call invoke( initial_u_kernel_type(r_u, chi, initial_time, qr) )
      call solver_algorithm(u, r_u)

      if(sample_init_rho) then
        call invoke_initial_rho_sample_kernel( rho, chi, initial_time )
      else
        call invoke( set_rho_kernel_type ( rho, chi, initial_time, qr ) )
      end if

      call log_event( "Gungho: Mapping rho->rho_in_wth in same space as theta..", LOG_LEVEL_INFO )

      call map_rho_to_theta(rho, theta, rho_in_wth)      
      if (use_moisture) call invoke_initial_mr_kernel( theta, rho_in_wth, mr(:), chi )

      call log_event( "Gungho: Initialised prognostic fields", LOG_LEVEL_INFO )

    else                                   ! Recorded check point to start from

      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("rho"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      call rho%read_restart(trim(restart%startfname("rho")))

      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("u"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      call u%read_restart(trim(restart%startfname("u")))

      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("theta"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      call theta%read_restart(trim(restart%startfname("theta")))

      write(log_scratch_space,'(A,A)') "Reading file:", &
            trim(restart%startfname("xi"))
      call log_event(log_scratch_space,LOG_LEVEL_INFO)

      call xi%read_restart(trim(restart%startfname("xi")))

      if (use_moisture)then
        do i=1,nummr
          write(name, '(A,I2.2)') 'mr_', i
          call mr(i)%read_restart(trim(restart%startfname(name)))
        end do
      end if


      call log_event( "Gungho: Mapping rho->rho_in_wth in same space as theta..", LOG_LEVEL_INFO )
      call map_rho_to_theta(rho, theta, rho_in_wth)

      call log_event( "Gungho: Read prognostic fields from files", LOG_LEVEL_INFO )
    end if

    !==========================================================================!

  end subroutine init_prognostic_fields_alg

end module init_prognostic_fields_alg_mod
