!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------
!> @brief Computes the boundary integral part of the projection operator from
!>        the velocity space to the potential temperature space weighted by
!>        the potential temperature gradient.
!>
!> Kernel which computes the boundary integral part of the projection operator
!> from the velocity space to the potential temperature space weighted by the
!> potential temperature gradient Compute the projection operator
!> \f<[<\gamma, flux(\theta*v)\cdot n>\f] where v is in W2 and gamma is in the
!> potential temperature space.
!>
module weighted_proj_theta2_bd_kernel_mod

  use argument_mod,      only : arg_type, func_type, mesh_data_type, &
                                GH_OPERATOR, GH_FIELD, GH_REAL,      &
                                GH_READ, GH_READWRITE,               &
                                GH_BASIS,                            &
                                CELLS, GH_QUADRATURE_face,           &
                                adjacent_face,                       &
                                reference_element_normal_to_face,    &
                                reference_element_out_face_normal
  use constants_mod,     only : r_def, i_def, l_def
  use cross_product_mod, only : cross_product
  use fs_continuity_mod, only : W2, Wtheta
  use kernel_mod,        only : kernel_type
  use planet_config_mod, only : cp

  implicit none

  !---------------------------------------------------------------------------
  ! Public types
  !---------------------------------------------------------------------------
  !> The type declaration for the kernel. Contains the metadata needed by the
  !> Psy layer.
  !>
  type, public, extends(kernel_type) :: weighted_proj_theta2_bd_kernel_type
    private
    type(arg_type) :: meta_args(3) = (/                            &
      arg_type(GH_OPERATOR, GH_READWRITE, Wtheta, W2),             &
      arg_type(GH_FIELD,    GH_READ,      Wtheta),                 &
      arg_type(GH_REAL,     GH_READ)                               &
        /)
    type(func_type) :: meta_funcs(2) = (/                          &
      func_type(Wtheta, GH_BASIS),                                 &
      func_type(W2,     GH_BASIS)                                  &
        /)
    integer :: iterates_over = CELLS
    integer :: gh_shape = GH_QUADRATURE_face
    type(mesh_data_type) :: meta_init(3) = (/                       &
      mesh_data_type( adjacent_face ),                              &
      mesh_data_type( reference_element_normal_to_face ),           &
      mesh_data_type( reference_element_out_face_normal )           &
     /)
  contains
    procedure, nopass ::weighted_proj_theta2_bd_code
  end type

  !---------------------------------------------------------------------------
  ! Contained functions/subroutines
  !---------------------------------------------------------------------------
  public weighted_proj_theta2_bd_code

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  !> @brief The subroutine which is called directly by the Psy layer
  !!
  !! @param[in] cell Cell number
  !! @param[in] nlayers Integer the number of layers
  !! @param[in] ncell_3d ncell*ndf
  !! @param[inout] projection Projection operator to compute
  !! @param[in] theta Potential temperature
  !! @param[in] scalar Real to scale matrix by
  !! @param[in] ndf_w2 Number of degrees of freedom per cell for w2
  !! @param[in] ndf_wtheta Number of degrees of freedom per cell for wtheta
  !! @param[in] undf_wtheta Number of unique of degrees of freedom for wtheta
  !! @param[in] stencil_wtheta_map Stencil dofmap for the Wtheta space
  !! @param[in] stencil_wtheta_size Number of cells in the Wtheta stencil map
  !! @param[in] nqp Integer, number of quadrature points
  !! @param[in] wqp Real array. Quadrature weights
  !! @param[in] w2_basis_face Real 4-dim array holding w2 basis functions
  !!            evaluated at Gaussian quadrature points on horizontal faces
  !! @param[in] wtheta_basis_face Real 4-dim array holding wtheta basis functions
  !!            evaluated at Gaussian quadrature points on horizontal faces
  !! @param[in] adjacent_face Vector containing information on neighbouring face
  !!            index for the current cell
  !! @param[in] normal_to_face Vector of normal to reference element faces.
  !! @param[in] out_face_normal Vector of normal to reference element "out
  !!                            faces".
  !!
  subroutine weighted_proj_theta2_bd_code( cell, nlayers, ncell_3d, &
                                           projection,              &
                                           theta,                   &
                                           scalar,                  &
                                           ndf_w2,                  &
                                           ndf_wtheta, undf_wtheta, &
                                           stencil_wtheta_map,      &
                                           stencil_wtheta_size,     &
                                           nqp, wqp,                &
                                           w2_basis_face,           &
                                           wtheta_basis_face,       &
                                           adjacent_face,           &
                                           normal_to_face, out_face_normal )

    implicit none

    ! Arguments
    integer(kind=i_def), intent(in) :: cell, nlayers, ncell_3d, nqp
    integer(kind=i_def), intent(in) :: ndf_wtheta, undf_wtheta, ndf_w2
    integer(kind=i_def), intent(in) :: stencil_wtheta_size

    integer(kind=i_def), dimension(ndf_wtheta, stencil_wtheta_size), intent(in) :: stencil_wtheta_map

    real(kind=r_def), dimension(ndf_wtheta,ndf_w2,ncell_3d),    intent(inout) :: projection
    real(kind=r_def), dimension(undf_wtheta),                   intent(in)    :: theta
    real(kind=r_def),                                           intent(in)    :: scalar
    real(kind=r_def), dimension(3,ndf_w2,    nqp,4), intent(in)    :: w2_basis_face
    real(kind=r_def), dimension(1,ndf_wtheta,nqp,4), intent(in)    :: wtheta_basis_face
    real(kind=r_def), dimension(nqp,4),              intent(in)    :: wqp

    integer(kind=i_def), intent(in) :: adjacent_face(:)
    real(kind=r_def),    intent(in) :: normal_to_face(:,:)
    real(kind=r_def),    intent(in) :: out_face_normal(:,:)

    ! Internal variables
    integer(kind=i_def) :: df, k, ik, face, face_next, dft, df2
    integer(kind=i_def) :: qp

    real(kind=r_def), dimension(ndf_wtheta) :: theta_e, theta_next_e
    real(kind=r_def) :: theta_at_fquad, theta_next_at_fquad, v_dot_n
    real(kind=r_def) :: flux_term, theta_av

    logical(kind=l_def), parameter :: upwind = .false.

    ! Assumes same number of horizontal qp in x and y
    do k = 0, nlayers-1
      ik = k + 1 + (cell-1)*nlayers
      do face = 1, size( adjacent_face, 1 )
        ! Storing opposite face number on neighbouring cell
        face_next = adjacent_face(face)

        ! Computing theta in adjacent cells
        do df = 1, ndf_wtheta
          theta_e(df)      = theta(stencil_wtheta_map(df, 1) + k )
          theta_next_e(df) = theta(stencil_wtheta_map(df, face+1) + k )
        end do

        do qp = 1, nqp

          theta_at_fquad = 0.0_r_def
          theta_next_at_fquad = 0.0_r_def
          do df = 1, ndf_wtheta
            theta_at_fquad       = theta_at_fquad      + theta_e(df)     *wtheta_basis_face(1,df,qp,face)
            theta_next_at_fquad  = theta_next_at_fquad + theta_next_e(df)*wtheta_basis_face(1,df,qp,face_next)
          end do
          theta_av = 0.5_r_def * (theta_at_fquad + theta_next_at_fquad)

          do df2 = 1,ndf_w2
            v_dot_n = dot_product(w2_basis_face(:,df2,qp,face),out_face_normal(:, face))
            flux_term = wqp(qp,face) * theta_av * v_dot_n
            if (upwind) then
              flux_term = flux_term + 0.5_r_def * abs(v_dot_n) * &
                          (theta_at_fquad - theta_next_at_fquad)
            end if
            do dft = 1,ndf_wtheta
              projection(dft,df2,ik) = projection(dft,df2,ik) &
                                     + wtheta_basis_face(1,dft,qp,face) &
                                     * flux_term * scalar

            end do ! dft
          end do ! df2
        end do ! qp
      end do ! faces
    end do ! layers

  end subroutine weighted_proj_theta2_bd_code

end module weighted_proj_theta2_bd_kernel_mod
