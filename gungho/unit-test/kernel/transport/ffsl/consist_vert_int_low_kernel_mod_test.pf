!-----------------------------------------------------------------------------
! (C) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> This code tests the vertical integer and low order flux kernel

module consist_vert_int_low_kernel_mod_test

  use constants_mod, only : i_def, r_tran
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, public, extends(TestCase) :: consist_vert_int_low_test_type
    private
  contains
    procedure test_all
  end type consist_vert_int_low_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use, intrinsic :: iso_fortran_env,  only : real64
    use consist_vert_int_low_kernel_mod,        only : consist_vert_int_low_code

    implicit none

    class(consist_vert_int_low_test_type), intent(inout) :: this

    real(r_tran),   parameter :: tol       = 1.0e-12_r_tran   ! r_tran 64bit
    integer(i_def), parameter :: nlayers   = 5
    integer(i_def), parameter :: ndf_w3    = 1
    integer(i_def), parameter :: ndf_w2v   = 2
    integer(i_def), parameter :: undf_w3   = nlayers
    integer(i_def), parameter :: undf_w2v  = nlayers+1
    integer(i_def), dimension(ndf_w3)  :: map_w3
    integer(i_def), dimension(ndf_w2v) :: map_w2v

    real(r_tran), dimension(undf_w2v) :: flux_low
    real(r_tran), dimension(undf_w2v) :: flux_int
    real(r_tran), dimension(undf_w2v) :: frac_dry_flux

    real(r_tran) :: field(5)
    real(r_tran) :: rho_d(5)
    real(r_tran) :: dep_pts(6)
    real(r_tran) :: detj(5)
    real(r_tran) :: dt
    real(r_tran) :: answer_low(1:6)
    real(r_tran) :: answer_int(1:6)

    real(r_tran)   :: use_tol
    integer(i_def) :: k

    ! Set up maps
    map_w3(1)= 1
    map_w2v(:) = (/ 1, 2 /)

    ! Set constant grid spacing for tests
    rho_d = (/ 1.5_r_tran, 1.5_r_tran, 1.5_r_tran, 1.5_r_tran, 1.5_r_tran /)
    detj = (/ 2.0_r_tran, 2.0_r_tran, 2.0_r_tran, 2.0_r_tran, 2.0_r_tran /)
    dt = 1.0_r_tran

    ! Use linear field
    field = (/ 1.0_r_tran, 2.0_r_tran, 3.0_r_tran, 4.0_r_tran, 5.0_r_tran /)
    dep_pts = (/ 0.0_r_tran, 0.5_r_tran, 1.2_r_tran, -1.0_r_tran, -0.1_r_tran, 0.0_r_tran /)
    frac_dry_flux = (/ 0.0_r_tran, 3.0_r_tran, 6.0_r_tran, 0.0_r_tran, -3.0_r_tran, 0.0_r_tran /)
    answer_low = (/ 0.0_r_tran,  3.0_r_tran,  6.0_r_tran,  0.0_r_tran,  -15.0_r_tran,  0.0_r_tran /)
    answer_int = (/ 0.0_r_tran,  0.0_r_tran,  6.0_r_tran,  -12.0_r_tran,  0.0_r_tran,  0.0_r_tran /)


    call consist_vert_int_low_code( nlayers,       &
                                    flux_low,      &
                                    flux_int,      &
                                    frac_dry_flux, &
                                    dep_pts,       &
                                    detj,          &
                                    field,         &
                                    rho_d,         &
                                    dt,            &
                                    ndf_w2v,       &
                                    undf_w2v,      &
                                    map_w2v,       &
                                    ndf_w3,        &
                                    undf_w3,       &
                                    map_w3  )

    if ( r_tran == real64 ) then
      use_tol = tol
    else
      ! Set tolerance based on precision of field
      use_tol = 10.0_r_tran*spacing( maxval(field(:)) )
    endif

    do k=1,nlayers+1
      @assertEqual( answer_low(k), flux_low(k), use_tol)
      @assertEqual( answer_int(k), flux_int(k), use_tol)
    end do

  end subroutine test_all

end module consist_vert_int_low_kernel_mod_test
